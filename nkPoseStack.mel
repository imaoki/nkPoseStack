/*! © 2023 imaoki | MIT License | https://github.com/imaoki */

// /////////////////////////////////////////////////////////////////////////////
// 汎用プロシージャ
// /////////////////////////////////////////////////////////////////////////////
// -----------------------------------------------------------------------------
// 数値
// -----------------------------------------------------------------------------
/*-
@param $value <float>
@param $min <float>
@param $max <float>
@returns <int>
*/
proc int betweenFloat(float $value, float $min, float $max) {
  return ($min <= $value && $value <= $max);
}

/*-
@param $value <int>
@param $min <int>
@param $max <int>
@returns <int>
*/
proc int betweenInt(int $value, int $min, int $max) {
  return ($min <= $value && $value <= $max);
}

// -----------------------------------------------------------------------------
// 文字列
// -----------------------------------------------------------------------------
/*-
@param $procName <string>
@returns <string>
*/
proc string getSourceFilename(string $procName) {
  string $filename;
  string $thisIs = `whatIs $procName`;
  string $regex1 = "^Mel procedure found in: *";
  string $regex2 = "^Script found in: *";
  if (`match $regex1 $thisIs` != "") {
    $filename = `substitute $regex1 $thisIs ""`;
  }
  else if (`match $regex2 $thisIs` != "") {
    $filename = `substitute $regex2 $thisIs ""`;
  }
  return $filename;
}

/*-
@param $procName <string>
@returns <string>
*/
proc string getSourceDirname(string $procName) {
  return dirname(getSourceFilename($procName));
}

// -----------------------------------------------------------------------------
// 配列
// -----------------------------------------------------------------------------
/*-
@param $a <float[]>
@param $b <float[]>
@returns <int>
*/
proc int minFloatArrayCount(float $a[], float $b[]) {
  int $numA = `size $a`;
  int $numB = `size $b`;
  return `min $numA $numB`;
}

/*-
@param $a <int[]>
@param $b <int[]>
@returns <int>
*/
proc int minIntArrayCount(int $a[], int $b[]) {
  int $numA = `size $a`;
  int $numB = `size $b`;
  return `min $numA $numB`;
}

/*-
@param $a <string[]>
@param $b <string[]>
@returns <int>
*/
proc int minStringArrayCount(string $a[], string $b[]) {
  int $numA = `size $a`;
  int $numB = `size $b`;
  return `min $numA $numB`;
}

// -----------------------------------------------------------------------------
// クォータニオン
// -----------------------------------------------------------------------------
/*-
@param $degree <float>
@param $axis <float[]>
@returns <float[]>
@remarks https://github.com/mrdoob/three.js/blob/dev/LICENSE
*/
proc float[] angleAxisToQuat(float $degree, float $axis[]) {
  float $ha = `deg_to_rad $degree` * 0.5;
  float $s = sin($ha);
  float $qx = $axis[0] * $s;
  float $qy = $axis[1] * $s;
  float $qz = $axis[2] * $s;
  float $qw = cos($ha);
  return {$qx, $qy, $qz, $qw};
}

/*-
@param $order <int>
@returns <int>
*/
proc int reverseRotateOrder(int $order) {
  int $reverseOrder[] = {5, 3, 4, 1, 2, 0};
  return $reverseOrder[$order];
}

/*-
@param $r <float[]>
@param $order <int>
@returns <float[]>
@remarks https://github.com/mrdoob/three.js/blob/dev/LICENSE
*/
proc float[] eulerToQuat(float $r[], int $order) {
  float $qx = 0.0;
  float $qy = 0.0;
  float $qz = 0.0;
  float $qw = 1.0;
  float $rx = `deg_to_rad $r[0]`;
  float $ry = `deg_to_rad $r[1]`;
  float $rz = `deg_to_rad $r[2]`;
  float $cx = cos(0.5 * $rx);
  float $cy = cos(0.5 * $ry);
  float $cz = cos(0.5 * $rz);
  float $sx = sin(0.5 * $rx);
  float $sy = sin(0.5 * $ry);
  float $sz = sin(0.5 * $rz);
  switch ($order) {
    // XYZ
    case 0:
      $qx = $sx * $cy * $cz + $cx * $sy * $sz;
      $qy = $cx * $sy * $cz - $sx * $cy * $sz;
      $qz = $cx * $cy * $sz + $sx * $sy * $cz;
      $qw = $cx * $cy * $cz - $sx * $sy * $sz;
      break;
    // YZX
    case 1:
      $qx = $sx * $cy * $cz + $cx * $sy * $sz;
      $qy = $cx * $sy * $cz + $sx * $cy * $sz;
      $qz = $cx * $cy * $sz - $sx * $sy * $cz;
      $qw = $cx * $cy * $cz - $sx * $sy * $sz;
      break;
    // ZXY
    case 2:
      $qx = $sx * $cy * $cz - $cx * $sy * $sz;
      $qy = $cx * $sy * $cz + $sx * $cy * $sz;
      $qz = $cx * $cy * $sz + $sx * $sy * $cz;
      $qw = $cx * $cy * $cz - $sx * $sy * $sz;
      break;
    // XZY
    case 3:
      $qx = $sx * $cy * $cz - $cx * $sy * $sz;
      $qy = $cx * $sy * $cz - $sx * $cy * $sz;
      $qz = $cx * $cy * $sz + $sx * $sy * $cz;
      $qw = $cx * $cy * $cz + $sx * $sy * $sz;
      break;
    // YXZ
    case 4:
      $qx = $sx * $cy * $cz + $cx * $sy * $sz;
      $qy = $cx * $sy * $cz - $sx * $cy * $sz;
      $qz = $cx * $cy * $sz - $sx * $sy * $cz;
      $qw = $cx * $cy * $cz + $sx * $sy * $sz;
      break;
    // ZYX
    case 5:
      $qx = $sx * $cy * $cz - $cx * $sy * $sz;
      $qy = $cx * $sy * $cz + $sx * $cy * $sz;
      $qz = $cx * $cy * $sz - $sx * $sy * $cz;
      $qw = $cx * $cy * $cz + $sx * $sy * $sz;
      break;
    default: break;
  }
  return {$qx, $qy, $qz, $qw};
}

/*-
@param $q <float[]>
@param $order <int>
@returns <float[]>
@remarks https://github.com/aadebdeb/MatrixQuaternionEulerAngleConversions
*/
proc float[] quatToEuler(float $q[], int $order) {
  float $rx = 0.0;
  float $ry = 0.0;
  float $rz = 0.0;
  float $qx = $q[0];
  float $qy = $q[1];
  float $qz = $q[2];
  float $qw = $q[3];
  float $sx;
  float $sy;
  float $sz;
  int $unlocked;
  switch ($order) {
    // XYZ
    case 0:
      $sy = 2 * $qx * $qz + 2 * $qy * $qw;
      $unlocked = abs($sy) < 0.9999999;
      $rx = $unlocked ? atan2(-(2 * $qy * $qz - 2 * $qx * $qw), 2 * $qw * $qw + 2 * $qz * $qz - 1)
          : atan2(2 * $qy * $qz + 2 * $qx * $qw, 2 * $qw * $qw + 2 * $qy * $qy - 1);
      $ry = asin($sy);
      $rz = $unlocked ? atan2(-(2 * $qx * $qy - 2 * $qz * $qw), 2 * $qw * $qw + 2 * $qx * $qx - 1) : 0;
      break;
    // YZX
    case 1:
      $sz = 2 * $qx * $qy + 2 * $qz * $qw;
      $unlocked = abs($sz) < 0.9999999;
      $rx = $unlocked ? atan2(-(2 * $qy * $qz - 2 * $qx * $qw), 2 * $qw * $qw + 2 * $qy * $qy - 1) : 0;
      $ry = $unlocked ? atan2(-(2 * $qx * $qz - 2 * $qy * $qw), 2 * $qw * $qw + 2 * $qx * $qx - 1)
          : atan2(2 * $qx * $qz + 2 * $qy * $qw, 2 * $qw * $qw + 2 * $qz * $qz - 1);
      $rz = asin($sz);
      break;
    // ZXY
    case 2:
      $sx = 2 * $qy * $qz + 2 * $qx * $qw;
      $unlocked = abs($sx) < 0.9999999;
      $rx = asin($sx);
      $ry = $unlocked ? atan2(-(2 * $qx * $qz - 2 * $qy * $qw), 2 * $qw * $qw + 2 * $qz * $qz - 1) : 0;
      $rz = $unlocked ? atan2(-(2 * $qx * $qy - 2 * $qz * $qw), 2 * $qw * $qw + 2 * $qy * $qy - 1)
          : atan2(2 * $qx * $qy + 2 * $qz * $qw, 2 * $qw * $qw + 2 * $qx * $qx - 1);
      break;
    // XZY
    case 3:
      $sz = -(2 * $qx * $qy - 2 * $qz * $qw);
      $unlocked = abs($sz) < 0.9999999;
      $rx = $unlocked ? atan2(2 * $qy * $qz + 2 * $qx * $qw, 2 * $qw * $qw + 2 * $qy * $qy - 1)
          : atan2(-(2 * $qy * $qz - 2 * $qx * $qw), 2 * $qw * $qw + 2 * $qz * $qz - 1);
      $ry = $unlocked ? atan2(2 * $qx * $qz + 2 * $qy * $qw, 2 * $qw * $qw + 2 * $qx * $qx - 1) : 0;
      $rz = asin($sz);
      break;
    // YXZ
    case 4:
      $sx = -(2 * $qy * $qz - 2 * $qx * $qw);
      $unlocked = abs($sx) < 0.9999999;
      $rx = asin($sx);
      $ry = $unlocked ? atan2(2 * $qx * $qz + 2 * $qy * $qw, 2 * $qw * $qw + 2 * $qz * $qz - 1)
          : atan2(-(2 * $qx * $qz - 2 * $qy * $qw), 2 * $qw * $qw + 2 * $qx * $qx - 1);
      $rz = $unlocked ? atan2(2 * $qx * $qy + 2 * $qz * $qw, 2 * $qw * $qw + 2 * $qy * $qy - 1) : 0;
      break;
    // ZYX
    case 5:
      $sy = -(2 * $qx * $qz - 2 * $qy * $qw);
      $unlocked = abs($sy) < 0.9999999;
      $rx = $unlocked ? atan2(2 * $qy * $qz + 2 * $qx * $qw, 2 * $qw * $qw + 2 * $qz * $qz - 1) : 0;
      $ry = asin($sy);
      $rz = $unlocked ? atan2(2 * $qx * $qy + 2 * $qz * $qw, 2 * $qw * $qw + 2 * $qx * $qx - 1)
          : atan2(-(2 * $qx * $qy - 2 * $qz * $qw), 2 * $qw * $qw + 2 * $qy * $qy - 1);
      break;
    default: break;
  }
  $rx = `rad_to_deg $rx`;
  $ry = `rad_to_deg $ry`;
  $rz = `rad_to_deg $rz`;
  return {$rx, $ry, $rz};
}

/*-
@param $q1 <float[]>
@param $q2 <float[]>
@returns <float[]>
*/
proc float[] quatAdd(float $q1[], float $q2[]) {
  float $q1x = $q1[0];
  float $q1y = $q1[1];
  float $q1z = $q1[2];
  float $q1w = $q1[3];
  float $q2x = $q2[0];
  float $q2y = $q2[1];
  float $q2z = $q2[2];
  float $q2w = $q2[3];
  float $qx = $q1x + $q2x;
  float $qy = $q1y + $q2y;
  float $qz = $q1z + $q2z;
  float $qw = $q1w + $q2w;
  return {$qx, $qy, $qz, $qw};
}

/*-
@param $q1 <float[]>
@param $q2 <float[]>
@returns <float[]>
*/
proc float[] quatSubtract(float $q1[], float $q2[]) {
  float $q1x = $q1[0];
  float $q1y = $q1[1];
  float $q1z = $q1[2];
  float $q1w = $q1[3];
  float $q2x = $q2[0];
  float $q2y = $q2[1];
  float $q2z = $q2[2];
  float $q2w = $q2[3];
  float $qx = $q1x - $q2x;
  float $qy = $q1y - $q2y;
  float $qz = $q1z - $q2z;
  float $qw = $q1w - $q2w;
  return {$qx, $qy, $qz, $qw};
}

/*-
@param $q1 <float[]>
@param $q2 <float[]>
@returns <float[]>
@remarks https://github.com/mrdoob/three.js/blob/dev/LICENSE
*/
proc float[] quatMultiply(float $q1[], float $q2[]) {
  float $q1x = $q1[0];
  float $q1y = $q1[1];
  float $q1z = $q1[2];
  float $q1w = $q1[3];
  float $q2x = $q2[0];
  float $q2y = $q2[1];
  float $q2z = $q2[2];
  float $q2w = $q2[3];
  float $qx = $q1x * $q2w + $q1w * $q2x + $q1y * $q2z - $q1z * $q2y;
  float $qy = $q1y * $q2w + $q1w * $q2y + $q1z * $q2x - $q1x * $q2z;
  float $qz = $q1z * $q2w + $q1w * $q2z + $q1x * $q2y - $q1y * $q2x;
  float $qw = $q1w * $q2w - $q1x * $q2x - $q1y * $q2y - $q1z * $q2z;
  return {$qx, $qy, $qz, $qw};
}

/*-
@param $q <float[]>
@returns <float[]>
*/
proc float[] quatInverse(float $q[]) {
  return {$q[0] * -1, $q[1] * -1, $q[2] * -1, $q[3]};
}

/*-
@param $v <float[]>
@param $q <float[]>
@returns <float[]>
*/
proc float[] rotateVectorByQuat(float $v[], float $q[]) {
  float $iq[] = quatInverse($q);
  float $vq[] = {$v[0], $v[1], $v[2], 0};
  $vq = quatMultiply(quatMultiply($q, $vq), $iq);
  return {$vq[0], $vq[1], $vq[2]};
}

/*-
@param $q <float[]>
@param $axis <float[]>
@returns <float[]>
*/
proc float[] quatFlip(float $q[], float $axis[]) {
  float $flipQ[] = angleAxisToQuat(180.0, $axis);
  return quatMultiply($flipQ, $q);
}

// -----------------------------------------------------------------------------
// ノード
// -----------------------------------------------------------------------------
/*-
@returns <string[]>
*/
proc string[] getSelectedNodes() {
  return `ls -l -sl -typ "joint" -typ "transform"`;
}

/*-
@param $n <string>
@returns <string>
*/
proc string getParentNode(string $n) {
  string $parent;
  string $parents[] = `listRelatives -f -p $n`;
  if (`size $parents` > 0) $parent = $parents[0];
  return $parent;
}

// -----------------------------------------------------------------------------
// UIコントロール
// -----------------------------------------------------------------------------
/*-
@var <string[]> 共有のUIコントロール配列。
*/
global string $nkUIControls[];

/*-
@param $control <string[]>
@returns <>
*/
proc nkUIAppendControls(string $controls[]) {
  // print("nkUIAppendControls\n"); // debug
  global string $nkUIControls[];
  for ($control in $controls) {
    $nkUIControls[`size $nkUIControls`] = $control;
  }
  // print("  nkUIControls:{\n    " + stringArrayToString($nkUIControls, ",\n    ") + "\n  }\n"); // debug
}

/*-
@param $root <string>
@param $end <string> 同じウィンドウのコントロールを一括除去する場合は空文字列を指定する。
@returns <>
*/
proc nkUIRemoveControls(string $root, string $end) {
  // print("nkUIRemoveControls\n"); // debug
  global string $nkUIControls[];
  string $paths[];
  for ($control in $nkUIControls) {
    string $buffer[];
    int $depth = `tokenize $control "|" $buffer`;
    string $first = ($depth > 0) ? $buffer[0] : "";
    string $last = ($depth > 0) ? $buffer[$depth - 1] : "";
    int $shouldRemove = (!`size $end`)
        ? $first == $root
        : $first == $root && $last == $end;
    if ($shouldRemove) $paths[`size $paths`] = $control;
  }
  if (`size $paths` > 0) {
    $nkUIControls = stringArrayRemove($paths, $nkUIControls);
  }
  // print("  nkUIControls:{\n    " + stringArrayToString($nkUIControls, ",\n    ") + "\n  }\n"); // debug
}

/*-
@param $root <string>
@param $end <string> ウィンドウの場合は`$root`と同じ値を指定する。
@returns <string>
*/
proc string nkUIGetControl(string $root, string $end) {
  global string $nkUIControls[];
  string $path;
  for ($control in $nkUIControls) {
    string $buffer[];
    int $depth = `tokenize $control "|" $buffer`;
    string $first = ($depth > 0) ? $buffer[0] : "";
    string $last = ($depth > 0) ? $buffer[$depth - 1] : "";
    if ($first == $root && $last == $end) {
      $path = $control;
      break;
    }
  }
  return $path;
}

/*-
@param $control <string>
@returns <int>
*/
proc int[] checkBoxGrpGetEnable(string $control) {
  return `checkBoxGrp -q -en $control`;
}

/*-
@param $enable <int>
@param $control <string>
@returns <>
*/
proc checkBoxGrpSetEnable(int $enable, string $control) {
  checkBoxGrp -e -en $enable $control;
}

/*-
@param $ncb <int> チェックボックスの数。1から4まで。
@param $control <string>
@returns <int[]>
*/
proc int[] checkBoxGrpGetValues(int $ncb, string $control) {
  $ncb = `clamp 1 4 $ncb`;
  int $values[];
  if ($ncb > 0) $values[0] = `checkBoxGrp -q -v1 $control`;
  if ($ncb > 1) $values[1] = `checkBoxGrp -q -v2 $control`;
  if ($ncb > 2) $values[2] = `checkBoxGrp -q -v3 $control`;
  if ($ncb > 3) $values[3] = `checkBoxGrp -q -v4 $control`;
  return $values;
}

/*-
@param $values <int[]>
@param $control <string>
@returns <>
*/
proc checkBoxGrpSetValues(int $values[], string $control) {
  int $ncb = `size $values`;
  if ($ncb > 0) checkBoxGrp -e -v1 $values[0] $control;
  if ($ncb > 1) checkBoxGrp -e -v2 $values[1] $control;
  if ($ncb > 2) checkBoxGrp -e -v3 $values[2] $control;
  if ($ncb > 3) checkBoxGrp -e -v4 $values[3] $control;
}

/*-
@param $control <string>
@returns <int>
*/
proc int radioButtonGrpGetEnable(string $control) {
  return `radioButtonGrp -q -en $control`;
}

/*-
@param $enable <int>
@param $control <string>
@returns <>
*/
proc radioButtonGrpSetEnable(int $enable, string $control) {
  radioButtonGrp -e -en $enable $control;
}

/*-
@param $control <string>
@returns <int> 基数を`0`に補正した値。
*/
proc int radioButtonGrpGetSelect(string $control) {
  return (`radioButtonGrp -q -sl $control` - 1);
}

/*-
@param $index <int> 基数`0`の整数。
@param $control <string>
@returns <>
*/
proc radioButtonGrpSetSelect(int $index, string $control) {
  radioButtonGrp -e -sl ($index + 1) $control;
}

/*-
@param $control <string>
@returns <int>
*/
proc int textFieldGrpGetEnable(string $control) {
  return `textFieldGrp -q -en $control`;
}

/*-
@param $enable <int>
@param $control <string>
@returns <>
*/
proc textFieldGrpSetEnable(int $enable, string $control) {
  textFieldGrp -e -en $enable $control;
}

/*-
@param $control <string>
@returns <int> 基数を`0`に補正した値。
*/
proc int textFieldGrpGetInsertionPosition(string $control) {
  return (`textFieldGrp -q -ip $control` - 1);
}

/*-
@param $position <int> 基数`0`の整数。`-1`を指定すると末尾にセットされる。
@param $control <string>
@returns <>
*/
proc textFieldGrpSetInsertionPosition(int $position, string $control) {
  textFieldGrp -e -ip ($position + 1) $control;
}

/*-
@param $control <string>
@returns <string>
*/
proc string textFieldGrpGetText(string $control) {
  return `textFieldGrp -q -tx $control`;
}

/*-
@param $text <string>
@param $control <string>
@returns <>
*/
proc textFieldGrpSetText(string $text, string $control) {
  textFieldGrp -e -tx $text $control;
}

/*-
@param $label <string>
@param $control <string>
@returns <>
*/
proc textScrollListAppend(string $label, string $control) {
  textScrollList -e -a $label $control;
}

/*-
@param $index <int> 基数`0`の整数。
@param $label <string>
@param $control <string>
@returns <>
*/
proc textScrollListAppendPosition(int $index, string $label, string $control) {
  textScrollList -e -ap ($index + 1) $label $control;
}

/*-
@param $control <string>
@returns <>
*/
proc textScrollListRemoveAll(string $control) {
  textScrollList -e -ra $control;
}

/*-
@param $index <int> 基数`0`の整数。
@param $control <string>
@returns <>
*/
proc textScrollListRemoveIndexedItem(int $index, string $control) {
  textScrollList -e -rii ($index + 1) $control;
}

/*-
@param $index <int> 基数`0`の整数。
@param $control <string>
@returns <>
*/
proc textScrollListSetSelectIndexedItem(int $index, string $control) {
  textScrollList -e -sii ($index + 1) $control;
}

/*-
@param $indices <int[]> 基数`0`の整数配列。
@param $control <string>
@returns <>
*/
proc textScrollListSetSelectIndexedItems(int $indices[], string $control) {
  for ($i in $indices) {
    textScrollList -e -sii ($i + 1) $control;
  }
}

/*-
@param $control <string>
@returns <int[]> 基数を`0`に補正した値。
*/
proc int[] textScrollListGetSelectIndexedItems(string $control) {
  int $indices[] = `textScrollList -q -sii $control`;
  for ($i = 0; $i < `size $indices`; $i++) $indices[$i] -= 1;
  return $indices;
}

/*-
@param $control <string>
@returns <int> 基数を`0`に補正した値。未選択の場合は`-1`を返す。
*/
proc int textScrollListGetSelectIndexedItem(string $control) {
  int $indices[] = textScrollListGetSelectIndexedItems($control);
  return ((`size $indices`) ? $indices[0] : -1);
}

/*-
@param $control <string>
@returns <>
*/
proc workspaceControlClose(string $control) {
  workspaceControl -e -cl $control;
}

/*-
@param $control <string>
@returns <int>
*/
proc int workspaceControlGetExists(string $control) {
  return `workspaceControl -q -ex $control`;
}

/*-
@param $control <string>
@returns <int>
*/
proc int workspaceControlGetFloating(string $control) {
  return `workspaceControl -q -fl $control`;
}

/*-
@param $state <int>
@param $control <string>
@returns <>
*/
proc workspaceControlSetFloating(int $state, string $control) {
  workspaceControl -e -fl $state $control;
}

/*-
@param $control <string>
@returns <int>
*/
proc int workspaceControlGetVisible(string $control) {
  return `workspaceControl -q -vis $control`;
}

// /////////////////////////////////////////////////////////////////////////////
// コアプロシージャ
// /////////////////////////////////////////////////////////////////////////////
/*-
@var <string[]>
```ebnf
            pose = '"' , pose_name , "=" , pose_transforms , '"' ;
       pose_name = { any_char - "=" } ;
 pose_transforms = { pose_transform }+ ;
  pose_transform = transform_header , transform_values , ";" ;
transform_header = transform_node , "[" , pose_setting , "]" ;
  transform_node = [ "|" ] , node_name , { "|" , node_name } ;
       node_name = letter , { letter | digit | "_" } ;
    pose_setting = paste_setting , "/" , select_setting , "/" , mirror_setting ;
   paste_setting = bool , "," , bool , "," , bool ;
  select_setting = select_pattern , "," , select_pattern ;
  mirror_setting = axis , "," , axis , "," , axis , "," , bool , "," , bool ;
  select_pattern = { letter | digit | "_" }+ ;
            axis = ? 0-2 ? ;
            bool = ? 0-1 ? ;
transform_values = t , "," , q , "," , s , "," , h ;
               t = number , "," , number , "," , number ;
               q = number , "," , number , "," , number , "," , number ;
               s = number , "," , number , "," , number ;
               h = number , "," , number , "," , number ;
          number = [ "-" ] , ( integer | real ) ;
           digit = ? 0-9 ? ;
          letter = ? a-zA-Z ? ;
        any_char = ? Any visible characters ? ;
```
*/
global string $nkPoseStackPoses[];

/*-
@var <int[]>
*/
global int $nkPoseStackInitPasteSetting[];

/*-
@var <string[]>
*/
global string $nkPoseStackInitSelectSetting[];

/*-
@var <int[]>
*/
global int $nkPoseStackInitMirrorSetting[];

/*-
@returns <>
*/
proc initializeInitSettingVariables() {
  // print("initializeInitSettingVariables\n"); // debug
  global int $nkPoseStackInitPasteSetting[];
  global string $nkPoseStackInitSelectSetting[];
  global int $nkPoseStackInitMirrorSetting[];
  if (!`size $nkPoseStackInitPasteSetting`) {
    $nkPoseStackInitPasteSetting = {1, 1, 0};
    // print("  nkPoseStackInitPasteSetting :{" + intArrayToString($nkPoseStackInitPasteSetting, ", ") + "}\n"); // debug
  }
  if (!`size $nkPoseStackInitSelectSetting`) {
    $nkPoseStackInitSelectSetting = {"_L", "_R"};
    // print("  nkPoseStackInitSelectSetting:{" + stringArrayToString($nkPoseStackInitSelectSetting, ", ") + "}\n"); // debug
  }
  if (!`size $nkPoseStackInitMirrorSetting`) {
    $nkPoseStackInitMirrorSetting = {0, 0, 1, 0, 0};
    // print("  nkPoseStackInitMirrorSetting:{" + intArrayToString($nkPoseStackInitMirrorSetting, ", ") + "}\n"); // debug
  }
}

// -----------------------------------------------------------------------------
// トランスフォーム
// -----------------------------------------------------------------------------
/*-
@param $n <string>
@param $poseSetting <string>
@returns <string>
*/
proc string makeTransformHeader(string $n, string $poseSetting) {
  return ($n + "[" + $poseSetting + "]");
}

/*-
@param $n <string>
@returns <float[]>
*/
proc float[] makeTransformValues(string $n) {
  // print("makeTransformValues\n"); // debug
  // print("  n:" + $n + "\n"); // debug
  float $values[] = {
    0, 0, 0,
    0, 0, 0, 1,
    1, 1, 1,
    0, 0, 0
  };
  string $type = `nodeType $n`;
  if ($type == "transform" || $type == "joint") {
    float $t[] = `getAttr ($n + ".translate")`;
    float $r[] = `getAttr ($n + ".rotate")`;
    float $s[] = `getAttr ($n + ".scale")`;
    float $h[] = `getAttr ($n + ".shear")`;
    // print("  t :{" + floatArrayToString($t, ", ") + "}\n"); // debug
    // print("  r :{" + floatArrayToString($r, ", ") + "}\n"); // debug
    // print("  s :{" + floatArrayToString($s, ", ") + "}\n"); // debug
    // print("  h :{" + floatArrayToString($h, ", ") + "}\n"); // debug

    int $o = `getAttr ($n + ".rotateOrder")`;
    $o = reverseRotateOrder($o);
    int $oXYZ = reverseRotateOrder(0);

    float $rq[] = eulerToQuat($r, $o);

    float $ra[] = `getAttr ($n + ".rotateAxis")`;
    $ra = eulerToQuat($ra, $oXYZ);

    float $jo[] = {0, 0, 0, 1};
    if (`attributeQuery -n $n -ex "jointOrient"`) {
      $jo = `getAttr ($n + ".jointOrient")`;
      $jo = eulerToQuat($jo, $oXYZ);
    }

    float $q[] = quatMultiply($jo, quatMultiply($ra, $rq));

    // print("  o :" + $o + "\n"); // debug
    // print("  rq:{" + floatArrayToString($rq, ", ") + "}\n"); // debug
    // print("  ra:{" + floatArrayToString($ra, ", ") + "}\n"); // debug
    // print("  jo:{" + floatArrayToString($jo, ", ") + "}\n"); // debug
    // print("  q :{" + floatArrayToString($q, ", ") + "}\n"); // debug

    $values = {
      $t[0], $t[1], $t[2],
      $q[0], $q[1], $q[2], $q[3],
      $s[0], $s[1], $s[2],
      $h[0], $h[1], $h[2]
    };
  }
  return $values;
}

/*-
@param $header <string>
@param $values <float[]>
@returns <string>
*/
proc string makePoseTransform(string $header, float $values[]) {
  return ($header + floatArrayToString($values, ",") + ";");
}

/*-
@param $nodes <string[]>
@param $poseSettings <string[]>
@returns <string[]>
*/
proc string[] generatePoseTransforms(string $nodes[], string $poseSettings[]) {
  // print("generatePoseTransforms\n"); // debug
  // print("  nodes       :{\n    " + stringArrayToString($nodes, ",\n    ") + "\n  }\n"); // debug
  // print("  poseSettings:{\n    " + stringArrayToString($poseSettings, ",\n    ") + "\n  }\n"); // debug
  string $poseTransforms[];
  int $numTransforms = minStringArrayCount($nodes, $poseSettings);
  for ($i = 0; $i < $numTransforms; $i++) {
    string $n = $nodes[$i];
    // print("  n:" + $n + "\n"); // debug
    string $header = makeTransformHeader($n, $poseSettings[$i]);
    float $values[] = makeTransformValues($n);
    string $poseTransform = makePoseTransform($header, $values);
    // print("    poseTransform:" + $poseTransform + "\n"); // debug
    $poseTransforms[$i] = $poseTransform;
  }
  return $poseTransforms;
}

/*-
@param $n <string>
@param $m <float[]>
@param $shouldSetT <int>
@param $shouldSetR <int>
@param $shouldSetS <int>
@returns <>
*/
proc setTransformAttributes(
  string $n,
  float $m[],
  int $shouldApplyT,
  int $shouldApplyR,
  int $shouldApplyS
) {
  // print("setTransformAttributes\n"); // debug
  // print("  n           :" + $n + "\n"); // debug
  // print("  m           :{" + floatArrayToString($m, ", ") + "}\n"); // debug
  // print("  shouldApplyT:" + $shouldApplyT + "\n"); // debug
  // print("  shouldApplyR:" + $shouldApplyR + "\n"); // debug
  // print("  shouldApplyS:" + $shouldApplyS + "\n"); // debug
  if (`objExists $n` && `size $m` == 12) {
    float $t[] = {$m[0], $m[1], $m[2]};
    float $r[] = {$m[3], $m[4], $m[5]};
    float $s[] = {$m[6], $m[7], $m[8]};
    float $h[] = {$m[9], $m[10], $m[11]};
    // print("  t:{" + floatArrayToString($t, ", ") + "}\n"); // debug
    // print("  r:{" + floatArrayToString($r, ", ") + "}\n"); // debug
    // print("  s:{" + floatArrayToString($s, ", ") + "}\n"); // debug
    // print("  h:{" + floatArrayToString($h, ", ") + "}\n"); // debug

    string $tx = $n + ".tx";
    string $ty = $n + ".ty";
    string $tz = $n + ".tz";
    string $rx = $n + ".rx";
    string $ry = $n + ".ry";
    string $rz = $n + ".rz";
    string $sx = $n + ".sx";
    string $sy = $n + ".sy";
    string $sz = $n + ".sz";

    if ($shouldApplyT) {
      if (!`getAttr -l $tx`) setAttr $tx $t[0];
      if (!`getAttr -l $ty`) setAttr $ty $t[1];
      if (!`getAttr -l $tz`) setAttr $tz $t[2];
    }
    if ($shouldApplyR) {
      if (!`getAttr -l $rx`) setAttr $rx $r[0];
      if (!`getAttr -l $ry`) setAttr $ry $r[1];
      if (!`getAttr -l $rz`) setAttr $rz $r[2];
    }
    if ($shouldApplyS) {
      if (!`getAttr -l $sx`) setAttr $sx $s[0];
      if (!`getAttr -l $sy`) setAttr $sy $s[1];
      if (!`getAttr -l $sz`) setAttr $sz $s[2];
    }
  }
}

/*-
@param $n <string>
@param $m <float[]>
@returns <float[]>
*/
proc float[] asIsTransform(string $n, float $m[]) {
  // print("asIsTransform\n"); // debug
  // print("  n :" + $n + "\n"); // debug
  // print("  m :{" + floatArrayToString($m, ", ") + "}\n"); // debug

  float $t[] = {$m[0], $m[1], $m[2]};
  float $q[] = {$m[3], $m[4], $m[5], $m[6]};
  float $s[] = {$m[7], $m[8], $m[9]};
  float $h[] = {$m[10], $m[11], $m[12]};
  // print("  t :{" + floatArrayToString($t, ", ") + "}\n"); // debug
  // print("  q :{" + floatArrayToString($q, ", ") + "}\n"); // debug
  // print("  s :{" + floatArrayToString($s, ", ") + "}\n"); // debug
  // print("  h :{" + floatArrayToString($h, ", ") + "}\n"); // debug

  int $o = `getAttr ($n + ".rotateOrder")`;
  $o = reverseRotateOrder($o);
  int $oXYZ = reverseRotateOrder(0);

  float $ra[] = `getAttr ($n + ".rotateAxis")`;
  $ra = quatInverse(eulerToQuat($ra, $oXYZ));

  float $jo[] = {0, 0, 0, 1};
  if (`attributeQuery -n $n -ex "jointOrient"`) {
    $jo = `getAttr ($n + ".jointOrient")`;
    $jo = quatInverse(eulerToQuat($jo, $oXYZ));
  }

  float $rq[] = quatMultiply($jo, quatMultiply($ra, $q));
  float $r[] = quatToEuler($rq, $o);

  // print("  o :" + $o + "\n"); // debug
  // print("  q :{" + floatArrayToString($q, ", ") + "}\n"); // debug
  // print("  ra:{" + floatArrayToString($ra, ", ") + "}\n"); // debug
  // print("  jo:{" + floatArrayToString($jo, ", ") + "}\n"); // debug
  // print("  rq:{" + floatArrayToString($rq, ", ") + "}\n"); // debug
  // print("  r :{" + floatArrayToString($r, ", ") + "}\n"); // debug

  float $values[] = {
    $t[0], $t[1], $t[2],
    $r[0], $r[1], $r[2],
    $s[0], $s[1], $s[2],
    $h[0], $h[1], $h[2]
  };
  // print("  values:{" + floatArrayToString($values, ", ") + "}\n"); // debug

  return $values;
}

/*-
@param $n <string>
@param $m <float[]>
@param $hasCommonParent <boolean>
@param $mirrorAxis <int>
@param $primaryAxis <int>
@param $secondaryAxis <int>
@param $invertPrimaryAxis <boolean>
@param $invertSecondaryAxis <boolean>
@returns <float[]>
*/
proc float[] mirrorTransform(
  string $n,
  float $m[],
  int $hasCommonParent,
  int $mirrorAxis,
  int $primaryAxis,
  int $secondaryAxis,
  int $invertPrimaryAxis,
  int $invertSecondaryAxis
) {
  // print("mirrorTransform\n"); // debug
  // print("  n                  :" + $n + "\n"); // debug
  // print("  m                  :{" + floatArrayToString($m, ", ") + "}\n"); // debug
  // print("  hasCommonParent    :" + $hasCommonParent + "\n"); // debug
  // print("  mirrorAxis         :" + $mirrorAxis + "\n"); // debug
  // print("  primaryAxis        :" + $primaryAxis + "\n"); // debug
  // print("  secondaryAxis      :" + $secondaryAxis + "\n"); // debug
  // print("  invertPrimaryAxis  :" + $invertPrimaryAxis + "\n"); // debug
  // print("  invertSecondaryAxis:" + $invertSecondaryAxis + "\n"); // debug

  float $t[] = {$m[0], $m[1], $m[2]};
  float $q[] = {$m[3], $m[4], $m[5], $m[6]};
  float $s[] = {$m[7], $m[8], $m[9]};
  float $h[] = {$m[10], $m[11], $m[12]};
  // print("  t :{" + floatArrayToString($t, ", ") + "}\n"); // debug
  // print("  q :{" + floatArrayToString($q, ", ") + "}\n"); // debug
  // print("  s :{" + floatArrayToString($s, ", ") + "}\n"); // debug
  // print("  h :{" + floatArrayToString($h, ", ") + "}\n"); // debug

  // 位置のミラーリング
  if ($hasCommonParent) {
    $t[$mirrorAxis] *= -1;
  }
  else {
    if ($invertPrimaryAxis) $t[$primaryAxis] *= -1;
    if ($invertSecondaryAxis) $t[$secondaryAxis] *= -1;
    if ($invertPrimaryAxis == $invertSecondaryAxis) {
      // 第三の軸を反転
      int $usedAxis[];
      $usedAxis[$primaryAxis] = 1;
      $usedAxis[$secondaryAxis] = 1;
      int $i;
      for ($i = 0; $i < 3; $i++) if (!$usedAxis[$i]) break;
      $t[$i] *= -1;
    }
  }

  // 回転のミラーリング
  int $o = `getAttr ($n + ".rotateOrder")`;
  $o = reverseRotateOrder($o);
  int $oXYZ = reverseRotateOrder(0);

  float $ra[] = `getAttr ($n + ".rotateAxis")`;
  $ra = quatInverse(eulerToQuat($ra, $oXYZ));

  float $jo[] = {0, 0, 0, 1};
  if (`attributeQuery -n $n -ex "jointOrient"`) {
    $jo = `getAttr ($n + ".jointOrient")`;
    $jo = quatInverse(eulerToQuat($jo, $oXYZ));
  }

  float $mq[] = $q;
  $mq[$mirrorAxis] *= -1;
  $mq[3] *= -1;
  float $cq[] = quatMultiply($jo, quatMultiply($ra, $mq));

  // print("  o :" + $o + "\n"); // debug
  // print("  q :{" + floatArrayToString($q, ", ") + "}\n"); // debug
  // print("  ra:{" + floatArrayToString($ra, ", ") + "}\n"); // debug
  // print("  jo:{" + floatArrayToString($jo, ", ") + "}\n"); // debug
  // print("  mq:{" + floatArrayToString($mq, ", ") + "}\n"); // debug

  // ソースとターゲットの親が異なる場合は親ノードの反転をリセットしておく
  if (!$hasCommonParent) {
    if ($mirrorAxis == $primaryAxis || $mirrorAxis == $secondaryAxis) {
      float $flipAxis[] = {0, 0, 0};
      if ($mirrorAxis == $primaryAxis) {
        $flipAxis[$secondaryAxis] = 1;
      }
      else if ($mirrorAxis == $secondaryAxis) {
        $flipAxis[$primaryAxis] = 1;
      }
      $cq = quatFlip($cq, $flipAxis);
    }
    if ($invertPrimaryAxis) {
      float $flipAxis[] = {0, 0, 0};
      $flipAxis[$secondaryAxis] = 1;
      $cq = quatFlip($cq, $flipAxis);
    }
    if ($invertSecondaryAxis) {
      float $flipAxis[] = {0, 0, 0};
      $flipAxis[$primaryAxis] = 1;
      $cq = quatFlip($cq, $flipAxis);
    }
  }

  // プライマリとセカンダリを対称化するための反転
  if ($mirrorAxis == $primaryAxis || $mirrorAxis == $secondaryAxis) {
    float $flipAxis[] = {0, 0, 0};
    if ($mirrorAxis == $primaryAxis) {
      $flipAxis[$secondaryAxis] = 1;
    }
    else if ($mirrorAxis == $secondaryAxis) {
      $flipAxis[$primaryAxis] = 1;
    }
    $flipAxis = rotateVectorByQuat($flipAxis, $cq);
    $cq = quatFlip($cq, $flipAxis);
  }

  // 任意の反転
  if ($invertPrimaryAxis) {
    float $flipAxis[] = {0, 0, 0};
    $flipAxis[$secondaryAxis] = 1;
    $flipAxis = rotateVectorByQuat($flipAxis, $cq);
    $cq = quatFlip($cq, $flipAxis);
  }
  if ($invertSecondaryAxis) {
    float $flipAxis[] = {0, 0, 0};
    $flipAxis[$primaryAxis] = 1;
    $flipAxis = rotateVectorByQuat($flipAxis, $cq);
    $cq = quatFlip($cq, $flipAxis);
  }

  float $r[] = quatToEuler($cq, $o);
  // print("  cq:{" + floatArrayToString($cq, ", ") + "}\n"); // debug
  // print("  r :{" + floatArrayToString($r, ", ") + "}\n"); // debug

  float $values[] = {
    $t[0], $t[1], $t[2],
    $r[0], $r[1], $r[2],
    $s[0], $s[1], $s[2],
    $h[0], $h[1], $h[2]
  };
  // print("  values:{" + floatArrayToString($values, ", ") + "}\n"); // debug

  return $values;
}

// -----------------------------------------------------------------------------
// データ変換
// -----------------------------------------------------------------------------
/*-
@param $pasteSetting <int[]>
@param $selectSetting <string[]>
@param $mirrorSetting <int[]>
@returns <string>
*/
proc string buildPoseSetting(
  int $pasteSetting[],
  string $selectSetting[],
  int $mirrorSetting[]
) {
  string $poseSetting = intArrayToString($pasteSetting, ",");
  $poseSetting += "/" + stringArrayToString($selectSetting, ",");
  $poseSetting += "/" + intArrayToString($mirrorSetting, ",");
  return $poseSetting;
}

/*-
@param $poseName <string>
@param $poseTransforms <string[]>
@returns <string>
*/
proc string buildPose(
  string $poseName,
  string $poseTransforms[]
) {
  string $pose = $poseName + "=";
  for ($poseTransform in $poseTransforms) {
    $pose += $poseTransform;
  }
  return $pose;
}

/*-
@param $pose <string>
@returns <string>
*/
proc string extractPoseName(string $pose) {
  string $poseName;
  string $buffer[];
  if (`tokenize $pose "=" $buffer` == 2) {
    $poseName = $buffer[0];
  }
  return $poseName;
}

/*-
@param $pose <string>
@returns <string[]>
*/
proc string[] extractPoseTransforms(string $pose) {
  string $poseTransforms[];
  string $buffer1[];
  if (`tokenize $pose "=" $buffer1` == 2) {
    string $buffer2[];
    if (`tokenize $buffer1[1] ";" $buffer2` > 0) {
      $poseTransforms = $buffer2;
    }
  }
  return $poseTransforms;
}

/*-
@param $poseTransform <string>
@returns <string[]>
*/
proc string[] extractTransformHeader(string $poseTransform) {
  string $header[];
  string $buffer1[];
  if (`tokenize $poseTransform "]" $buffer1` == 2) {
    string $buffer2[];
    if (`tokenize $buffer1[0] "[" $buffer2` == 2) {
      $header = $buffer2;
    }
  }
  return $header;
}

/*-
@param $poseTransform <string>
@returns <string>
*/
proc string extractTransformNode(string $poseTransform) {
  string $node;
  string $header[] = extractTransformHeader($poseTransform);
  if (`size $header` == 2) $node = $header[0];
  return $node;
}

/*-
@param $poseTransform <string>
@returns <string>
*/
proc string extractPoseSetting(string $poseTransform) {
  string $poseSetting;
  string $header[] = extractTransformHeader($poseTransform);
  if (`size $header` == 2) $poseSetting = $header[1];
  return $poseSetting;
}

/*-
@param $poseSetting <string>
@returns <string[]>
*/
proc string[] extractPoseSettings(string $poseSetting) {
  string $poseSettings[];
  string $buffer[];
  if (`tokenize $poseSetting "/" $buffer` == 3) {
    $poseSettings = $buffer;
  }
  return $poseSettings;
}

/*-
@param $poseSetting <string>
@returns <int[]>
*/
proc int[] extractPasteSetting(string $poseSetting) {
  int $pasteSetting[] = {1, 1, 0};
  string $poseSettings[] = extractPoseSettings($poseSetting);
  string $buffer[];
  if (`tokenize $poseSettings[0] "," $buffer` == 3) {
    for ($i = 0; $i < 3; $i++) {
      $pasteSetting[$i] = (int) $buffer[$i];
    }
  }
  return $pasteSetting;
}

/*-
@param $poseSetting <string>
@returns <string[]>
*/
proc string[] extractSelectSetting(string $poseSetting) {
  string $selectSetting[] = {"_L", "_R"};
  string $poseSettings[] = extractPoseSettings($poseSetting);
  string $buffer[];
  if (`tokenize $poseSettings[1] "," $buffer` == 2) {
    for ($i = 0; $i < 2; $i++) {
      $selectSetting[$i] = $buffer[$i];
    }
  }
  return $selectSetting;
}

/*-
@param $poseSetting <string>
@returns <int[]>
*/
proc int[] extractMirrorSetting(string $poseSetting) {
  int $mirrorSetting[] = {0, 0, 1, 0, 0};
  string $poseSettings[] = extractPoseSettings($poseSetting);
  string $buffer[];
  if (`tokenize $poseSettings[2] "," $buffer` == 5) {
    for ($i = 0; $i < 5; $i++) {
      $mirrorSetting[$i] = (int) $buffer[$i];
    }
  }
  return $mirrorSetting;
}

/*-
@param $poseTransform <string>
@returns <float[]>
*/
proc float[] extractTransformValues(string $poseTransform) {
  float $values[] = {
    0, 0, 0,
    0, 0, 0, 1,
    1, 1, 1,
    0, 0, 0
  };
  string $buffer1[];
  if (`tokenize $poseTransform "]" $buffer1` == 2) {
    string $buffer2[];
    if (`tokenize $buffer1[1] "," $buffer2` == 13) {
      for ($i = 0; $i < 13; $i++) {
        $values[$i] = (float) $buffer2[$i];
      }
    }
  }
  return $values;
}

// -----------------------------------------------------------------------------
// ポーズ
// -----------------------------------------------------------------------------
/*-
@returns <>
*/
proc clearPoses() {
  global string $nkPoseStackPoses[];
  clear $nkPoseStackPoses;
}

/*-
@returns <int>
*/
proc int getNumPoses() {
  global string $nkPoseStackPoses[];
  return `size $nkPoseStackPoses`;
}

/*-
@param $poseIndex <int> 基数`0`の整数。
@returns <string>
*/
proc string getPose(int $poseIndex) {
  global string $nkPoseStackPoses[];
  string $pose;
  if (betweenInt($poseIndex, 0, getNumPoses() - 1)) {
    $pose = $nkPoseStackPoses[$poseIndex];
  }
  return $pose;
}

/*-
@returns <string[]>
*/
proc string[] getPoseNames() {
  global string $nkPoseStackPoses[];
  string $poseNames[];
  for ($pose in $nkPoseStackPoses) {
    $poseNames[`size $poseNames`] = extractPoseName($pose);
  }
  return $poseNames;
}

/*-
@param $poseName <string>
@param $nodes <string[]>
@param $poseSettings <string[]>
@returns <int> 追加後のポーズ数。
*/
proc int addPose(
  string $poseName,
  string $nodes[],
  string $poseSettings[]
) {
  // print("addPose\n"); // debug
  // print("  poseName    :" + $poseName + "\n"); // debug
  // print("  nodes       :{\n    " + stringArrayToString($nodes, ",\n    ") + "\n  }\n"); // debug
  // print("  poseSettings:{\n    " + stringArrayToString($poseSettings, ",\n    ") + "\n  }\n"); // debug
  global string $nkPoseStackPoses[];
  string $poseTransforms[] = generatePoseTransforms($nodes, $poseSettings);
  // print("  poseTransforms:{\n    " + (stringArrayToString($poseTransforms, ",\n    ")) + "\n  }\n"); // debug
  string $pose = buildPose(
    $poseName,
    $poseTransforms
  );
  // print("  pose:" + $pose + "\n"); // debug
  $nkPoseStackPoses[(getNumPoses())] = $pose;
  return (getNumPoses());
}

/*-
@param $poseIndex <int> 基数`0`の整数。
@returns <int> 削除後のポーズ数。
*/
proc int deletePose(int $poseIndex) {
  // print("deletePose\n"); // debug
  // print("  poseIndex:" + $poseIndex + "\n"); // debug
  global string $nkPoseStackPoses[];
  if (betweenInt($poseIndex, 0, getNumPoses() - 1)) {
    stringArrayRemoveAtIndex($poseIndex, $nkPoseStackPoses);
  }
  return (getNumPoses());
}

/*-
@param $poseIndex <int> 基数`0`の整数。
@param $poseName <string>
@param $poseSettings <string[]>
@returns <>
*/
proc editPose(int $poseIndex, string $poseName, string $poseSettings[]) {
  // print("editPose\n"); // debug
  // print("  poseIndex   :" + $poseIndex + "\n"); // debug
  // print("  poseName    :" + $poseName + "\n"); // debug
  // print("  poseSettings:{\n    " + stringArrayToString($poseSettings, ",\n    ") + "\n  }\n"); // debug
  global string $nkPoseStackPoses[];

  string $pose = getPose($poseIndex);
  if (!`size $pose`) return;

  string $poseTransforms[] = extractPoseTransforms($pose);
  // print("  poseTransforms:{\n    " + stringArrayToString($poseTransforms, ",\n    ") + "\n  }\n"); // debug
  if (!`size $poseTransforms`) return;

  string $newPoseTransforms[];
  int $numTransforms = minStringArrayCount($poseSettings, $poseTransforms);
  for ($i = 0; $i < $numTransforms; $i++) {
    string $poseTransform = $poseTransforms[$i];
    // print("  poseTransform:" + $poseTransform + "\n"); // debug
    string $n = extractTransformNode($poseTransform);
    // print("    n               :" + $n + "\n"); // debug
    string $header = makeTransformHeader($n, $poseSettings[$i]);
    // print("    header          :" + $header + "\n"); // debug
    float $values[] = extractTransformValues($poseTransform);
    // print("    values          :{" + floatArrayToString($values, ", ") + "}\n"); // debug
    string $newPoseTransform = makePoseTransform($header, $values);
    // print("    newPoseTransform:" + $newPoseTransform + "\n"); // debug
    $newPoseTransforms[$i] = $newPoseTransform;
  }
  // print("  newPoseTransforms:{\n    " + (stringArrayToString($newPoseTransforms, ",\n    ")) + "\n  }\n"); // debug

  string $newPose = buildPose(
    $poseName,
    $newPoseTransforms
  );
  // print("  newPose:" + $newPose + "\n"); // debug
  $nkPoseStackPoses[$poseIndex] = $newPose;
}

/*-
@param $poseIndex <int> 基数`0`の整数。
@returns <int> 複製先のポーズインデックス。基数は`0`。
*/
proc int duplicatePose(int $poseIndex) {
  // print("duplicatePose\n"); // debug
  // print("  poseIndex:" + $poseIndex + "\n"); // debug
  global string $nkPoseStackPoses[];
  if (!betweenInt($poseIndex, 0, getNumPoses() - 1)) return $poseIndex;
  string $pose = getPose($poseIndex);
  if (!`size $pose`) return $poseIndex;
  int $newPoseIndex = $poseIndex + 1;
  // print("  newPoseIndex:" + $newPoseIndex + "\n"); // debug
  stringArrayInsertAtIndex($newPoseIndex, $nkPoseStackPoses, $pose);
  return $newPoseIndex;
}

/*-
@param $poseIndex <int> 基数`0`の整数。
@returns <int> 移動後のポーズインデックス。基数は`0`。
*/
proc int moveUpPose(int $poseIndex) {
  // print("moveUpPose\n"); // debug
  // print("  poseIndex:" + $poseIndex + "\n"); // debug
  global string $nkPoseStackPoses[];
  if (!betweenInt($poseIndex, 1, getNumPoses() - 1)) return $poseIndex;
  string $pose = getPose($poseIndex);
  if (!`size $pose`) return $poseIndex;
  int $newPoseIndex = $poseIndex - 1;
  // print("  newPoseIndex:" + $newPoseIndex + "\n"); // debug
  stringArrayRemoveAtIndex($poseIndex, $nkPoseStackPoses);
  stringArrayInsertAtIndex($newPoseIndex, $nkPoseStackPoses, $pose);
  return $newPoseIndex;
}

/*-
@param $poseIndex <int> 基数`0`の整数。
@returns <int> 移動後のポーズインデックス。基数は`0`。
*/
proc int moveDownPose(int $poseIndex) {
  // print("moveDownPose\n"); // debug
  // print("  poseIndex:" + $poseIndex + "\n"); // debug
  global string $nkPoseStackPoses[];
  if (!betweenInt($poseIndex, 0, getNumPoses() - 2)) return $poseIndex;
  string $pose = getPose($poseIndex);
  if (!`size $pose`) return $poseIndex;
  int $newPoseIndex = $poseIndex + 1;
  // print("  newPoseIndex:" + $newPoseIndex + "\n"); // debug
  stringArrayRemoveAtIndex($poseIndex, $nkPoseStackPoses);
  stringArrayInsertAtIndex($newPoseIndex, $nkPoseStackPoses, $pose);
  return $newPoseIndex;
}

/*-
@param $poseIndex <int> 基数`0`の整数。
@returns <>
*/
proc updatePose(int $poseIndex) {
  // print("updatePose\n"); // debug
  // print("  poseIndex:" + $poseIndex + "\n"); // debug
  global string $nkPoseStackPoses[];

  string $pose = getPose($poseIndex);
  if (!`size $pose`) return;

  string $poseName = extractPoseName($pose);
  // print("  poseName      :" + $poseName + "\n"); // debug
  string $poseTransforms[] = extractPoseTransforms($pose);
  // print("  poseTransforms:{\n    " + stringArrayToString($poseTransforms, ",\n    ") + "\n  }\n"); // debug
  if (!`size $poseTransforms`) return;

  string $nodes[];
  string $poseSettings[];
  for ($i = 0; $i < `size $poseTransforms`; $i++) {
    string $poseTransform = $poseTransforms[$i];
    // print("  poseTransform:" + $poseTransform + "\n"); // debug
    string $node = extractTransformNode($poseTransform);
    // print("    node       :" + $node + "\n"); // debug
    string $poseSetting = extractPoseSetting($poseTransform);
    // print("    poseSetting:" + $poseSetting + "\n"); // debug
    $nodes[$i] = $node;
    $poseSettings[$i] = $poseSetting;
  }
  string $newPoseTransforms[] = generatePoseTransforms($nodes, $poseSettings);
  // print("  newPoseTransforms:{\n    " + (stringArrayToString($newPoseTransforms, ",\n    ")) + "\n  }\n"); // debug

  string $newPose = buildPose(
    $poseName,
    $newPoseTransforms
  );
  // print("  newPose:" + $newPose + "\n"); // debug
  $nkPoseStackPoses[$poseIndex] = $newPose;
}

/*-
@param $poseIndex <int> 基数`0`の整数。
@param $isOpposite <boolean>
@returns <>
*/
proc selectNodes(int $poseIndex, int $isOpposite) {
  // print("selectNodes\n"); // debug
  // print("  poseIndex :" + $poseIndex + "\n"); // debug
  // print("  isOpposite:" + $isOpposite + "\n"); // debug

  string $pose = getPose($poseIndex);
  // print("  pose:" + $pose + "\n"); // debug
  if (!`size $pose`) return;

  string $poseTransforms[] = extractPoseTransforms($pose);
  // print("  poseTransforms:{\n    " + stringArrayToString($poseTransforms, ",\n    ") + "\n  }\n"); // debug
  if (!`size $poseTransforms`) return;

  string $targetNodes[];
  for ($poseTransform in $poseTransforms) {
    // print("  poseTransform:" + $poseTransform + "\n"); // debug
    string $targetNode = extractTransformNode($poseTransform);
    // print("    targetNode   :" + $targetNode + "\n"); // debug
    string $poseSetting = extractPoseSetting($poseTransform);
    // print("    poseSetting  :" + $poseSetting + "\n"); // debug
    string $selectSetting[] = extractSelectSetting($poseSetting);
    // print("    selectSetting:{" + stringArrayToString($selectSetting, ", ") + "}\n"); // debug
    string $patternA = $selectSetting[0];
    string $patternB = $selectSetting[1];
    // print("    patternA     :" + $patternA + "\n"); // debug
    // print("    patternB     :" + $patternB + "\n"); // debug
    if ($isOpposite) {
      if (`gmatch $targetNode ("*" + $patternA + "*")`) {
        $targetNode = substituteAllString($targetNode, $patternA, $patternB);
      }
      else if (`gmatch $targetNode ("*" + $patternB + "*")`) {
        $targetNode = substituteAllString($targetNode, $patternB, $patternA);
      }
    }
    if (`objExists $targetNode`) {
      $targetNodes[`size $targetNodes`] = $targetNode;
    }
  }
  // print("  targetNodes:{\n    " + stringArrayToString($targetNodes, "\n    ") + "\n  }\n"); // debug
  if (!`size $targetNodes`) return;

  int $mods = `getModifiers`;
  // print("  mods:" + $mods + "\n"); // debug
  switch ($mods) {
    // Nothing
    case 0: select -r $targetNodes; break;
    // Shift
    case 1: select -tgl $targetNodes; break;
    // Ctrl
    case 4: select -d $targetNodes; break;
    // Shift + Ctrl
    case 5: select -add $targetNodes; break;
    default: break;
  }
}

/*-
@param $poseIndex <int> 基数`0`の整数。
@param $isOpposite <boolean>
@param $targetNodes <string[]>
@returns <>
*/
proc pastePose(int $poseIndex, int $isOpposite, string $targetNodes[]) {
  // print("pastePose\n"); // debug
  // print("  poseIndex  :" + $poseIndex + "\n"); // debug
  // print("  isOpposite :" + $isOpposite + "\n"); // debug
  // print("  targetNodes:{\n    " + stringArrayToString($targetNodes, ",\n    ") + "\n  }\n"); // debug

  if (!`size $targetNodes`) return;

  string $pose = getPose($poseIndex);
  // print("  pose:" + $pose + "\n"); // debug
  if (!`size $pose`) return;

  string $poseTransforms[] = extractPoseTransforms($pose);
  // print("  poseTransforms:{\n    " + stringArrayToString($poseTransforms, ",\n    ") + "\n  }\n"); // debug
  if (!`size $poseTransforms`) return;

  int $numTargets = minStringArrayCount($targetNodes, $poseTransforms);
  // print("  numTargets:" + $numTargets + "\n"); // debug
  for ($i = 0; $i < $numTargets; $i++) {
    string $poseTransform = $poseTransforms[$i];
    // print("  poseTransform:" + $poseTransform + "\n"); // debug
    string $sourceNode = extractTransformNode($poseTransform);
    // print("    sourceNode     :" + $sourceNode + "\n"); // debug
    string $targetNode = $targetNodes[$i];
    // print("    targetNode     :" + $targetNode + "\n"); // debug

    float $transformValues[] = extractTransformValues($poseTransform);
    // print("    transformValues:{" + floatArrayToString($transformValues, ", ") + "}\n"); // debug

    string $poseSetting = extractPoseSetting($poseTransform);
    // print("    poseSetting    :" + $poseSetting + "\n"); // debug
    int $pasteSetting[] = extractPasteSetting($poseSetting);
    // print("    pasteSetting   :{" + intArrayToString($pasteSetting, ", ") + "}\n"); // debug

    if ($isOpposite) {
      int $mirrorSetting[] = extractMirrorSetting($poseSetting);
      // print("    mirrorSetting  :{" + intArrayToString($mirrorSetting, ", ") + "}\n"); // debug
      string $sourceParent = getParentNode($sourceNode);
      string $targetParent = getParentNode($targetNode);
      int $hasCommonParent = $sourceParent == $targetParent;
      // print("    hasCommonParent:" + $hasCommonParent + "\n"); // debug
      $transformValues = mirrorTransform(
        $targetNode,
        $transformValues,
        $hasCommonParent,
        $mirrorSetting[0],
        $mirrorSetting[1],
        $mirrorSetting[2],
        $mirrorSetting[3],
        $mirrorSetting[4]
      );
    }
    else {
      $transformValues = asIsTransform($targetNode, $transformValues);
    }

    setTransformAttributes(
      $targetNode,
      $transformValues,
      $pasteSetting[0],
      $pasteSetting[1],
      $pasteSetting[2]
    );
  }
}

// /////////////////////////////////////////////////////////////////////////////
// UI
// /////////////////////////////////////////////////////////////////////////////
/*-
@var <string>
*/
global string $nkPoseStackPromptPoseName;

/*-
@var <string[]>
*/
global string $nkPoseStackPromptNodes[];

/*-
@var <string[]>
*/
global string $nkPoseStackPromptPoseSettings[];

/*-
@var <int> 設定UIが更新中かどうか。
*/
global int $nkPoseStackPromptIsUIUpdating;

/*-
@var <int[]> プロンプトを閉じる際に初期化用の設定値を更新するかどうか。
*/
global int $nkPoseStackPromptNeedUpdateInitSetting[];

/*-
@returns <>
*/
proc initializePromptVariables() {
  global string $nkPoseStackPromptPoseName;
  global string $nkPoseStackPromptNodes[];
  global string $nkPoseStackPromptPoseSettings[];
  global int $nkPoseStackPromptIsUIUpdating;
  global int $nkPoseStackPromptNeedUpdateInitSetting[];
  $nkPoseStackPromptPoseName = "";
  clear $nkPoseStackPromptNodes;
  clear $nkPoseStackPromptPoseSettings;
  $nkPoseStackPromptIsUIUpdating = false;
  $nkPoseStackPromptNeedUpdateInitSetting = {false, false, false};
}

// -----------------------------------------------------------------------------
// UIコントロールイベント
// -----------------------------------------------------------------------------
/*-
@returns <string>
*/
proc string getParentWindow() {
  string $parentWindow = "MayaWindow";
  string $nkPoseStackWindow = nkUIGetControl("nkPoseStackWindow", "nkPoseStackWindow");
  if (workspaceControlGetFloating($nkPoseStackWindow)) {
    $parentWindow = $nkPoseStackWindow;
  }
  return $parentWindow;
}

/*-
@param $input <string>
@returns <string>
*/
proc string sanitizeText(string $input) {
  $input = substituteAllString($input, "=", "_");
  $input = substituteAllString($input, ";", "_");
  $input = substituteAllString($input, "[", "_");
  $input = substituteAllString($input, "]", "_");
  $input = substituteAllString($input, "/", "_");
  $input = substituteAllString($input, ",", "_");
  return $input;
}

/*-
@param $index <int> 基数`0`の整数。
@returns <string>
*/
proc string generateNodeListLabel(int $index) {
  global string $nkPoseStackPromptNodes[];
  global string $nkPoseStackPromptPoseSettings[];
  string $label = "[" + $nkPoseStackPromptPoseSettings[$index] + "] ";
  $label += $nkPoseStackPromptNodes[$index];
  return $label;
}

/*-
@param $partial <int> 選択した項目のみを更新するかどうか。
@returns <>
*/
proc updateNodeList(int $partial) {
  // print("updateNodeList\n"); // debug
  // print("  partial:" + $partial + "\n"); // debug
  global string $nkPoseStackPromptNodes[];
  global string $nkPoseStackPromptPoseSettings[];
  string $nodeList = nkUIGetControl("layoutDialog", "nodeList");
  int $nodeIndices[] = textScrollListGetSelectIndexedItems($nodeList);
  // print("  nodeIndices:{" + intArrayToString($nodeIndices, ", ") + "}\n"); // debug
  int $numTransforms = minStringArrayCount(
    $nkPoseStackPromptNodes,
    $nkPoseStackPromptPoseSettings
  );
  if ($partial) {
    for ($i = `size $nodeIndices`; $i > 0; $i--) {
      int $index = $nodeIndices[$i - 1];
      $label = generateNodeListLabel($index);
      textScrollListRemoveIndexedItem($index, $nodeList);
      textScrollListAppendPosition($index, $label, $nodeList);
    }
  }
  else {
    textScrollListRemoveAll($nodeList);
    for ($i = 0; $i < $numTransforms; $i++) {
      $label = generateNodeListLabel($i);
      textScrollListAppend($label, $nodeList);
    }
  }
  textScrollListSetSelectIndexedItems($nodeIndices, $nodeList);
}

/*-
@param $id <int>
@returns <>
*/
proc updatePoseSettings(int $id) {
  // print("updatePoseSettings\n"); // debug
  // print("  id:" + $id + "\n"); // debug
  global string $nkPoseStackPromptNodes[];
  global string $nkPoseStackPromptPoseSettings[];
  global int $nkPoseStackPromptIsUIUpdating;

  // 設定UIの更新中はポーズ設定を更新しない
  // print("  nkPoseStackPromptIsUIUpdating:" + $nkPoseStackPromptIsUIUpdating + "\n"); // debug
  if ($nkPoseStackPromptIsUIUpdating) return;

  string $nodeList = nkUIGetControl("layoutDialog", "nodeList");
  string $pasteAttributesCheckBoxGrp = nkUIGetControl("layoutDialog", "pasteAttributesCheckBoxGrp");
  string $selectPatternAText = nkUIGetControl("layoutDialog", "selectPatternAText");
  string $selectPatternBText = nkUIGetControl("layoutDialog", "selectPatternBText");
  string $mirrorAxisRadioButton = nkUIGetControl("layoutDialog", "mirrorAxisRadioButton");
  string $primaryAxisRadioButton = nkUIGetControl("layoutDialog", "primaryAxisRadioButton");
  string $secondaryAxisRadioButton = nkUIGetControl("layoutDialog", "secondaryAxisRadioButton");
  string $invertAxisCheckBoxGrp = nkUIGetControl("layoutDialog", "invertAxisCheckBoxGrp");

  int $pasteSetting[] = checkBoxGrpGetValues(3, $pasteAttributesCheckBoxGrp);
  string $selectSetting[] = {
    sanitizeText(textFieldGrpGetText($selectPatternAText)),
    sanitizeText(textFieldGrpGetText($selectPatternBText))
  };
  int $invertAxisValues[] = checkBoxGrpGetValues(2, $invertAxisCheckBoxGrp);
  int $mirrorSetting[] = {
    radioButtonGrpGetSelect($mirrorAxisRadioButton),
    radioButtonGrpGetSelect($primaryAxisRadioButton),
    radioButtonGrpGetSelect($secondaryAxisRadioButton),
    $invertAxisValues[0],
    $invertAxisValues[1]
  };
  // print("  pasteSetting :{" + intArrayToString($pasteSetting, ", ") + "}\n"); // debug
  // print("  selectSetting:{" + stringArrayToString($selectSetting, ", ") + "}\n"); // debug
  // print("  mirrorSetting:{" + intArrayToString($mirrorSetting, ", ") + "}\n"); // debug

  int $nodeIndices[] = textScrollListGetSelectIndexedItems($nodeList);
  // print("  nodeIndices:{" + intArrayToString($nodeIndices, ", ") + "}\n"); // debug
  for ($nodeIndex in $nodeIndices) {
    string $newPoseSetting = $nkPoseStackPromptPoseSettings[$nodeIndex];
    // print("    newPoseSetting:" + $newPoseSetting + "\n"); // debug
    int $newPasteSetting[] = extractPasteSetting($newPoseSetting);
    string $newSelectSetting[] = extractSelectSetting($newPoseSetting);
    int $newMirrorSetting[] = extractMirrorSetting($newPoseSetting);
    // print("  newPasteSetting :{" + intArrayToString($newPasteSetting, ", ") + "}\n"); // debug
    // print("  newSelectSetting:{" + stringArrayToString($newSelectSetting, ", ") + "}\n"); // debug
    // print("  newMirrorSetting:{" + intArrayToString($newMirrorSetting, ", ") + "}\n"); // debug

    switch ($id) {
      case 0: $newPasteSetting[0] = $pasteSetting[0]; break;
      case 1: $newPasteSetting[1] = $pasteSetting[1]; break;
      case 2: $newPasteSetting[2] = $pasteSetting[2]; break;
      case 3: $newSelectSetting[0] = $selectSetting[0]; break;
      case 4: $newSelectSetting[1] = $selectSetting[1]; break;
      case 5: $newMirrorSetting[0] = $mirrorSetting[0]; break;
      case 6: $newMirrorSetting[1] = $mirrorSetting[1]; break;
      case 7: $newMirrorSetting[2] = $mirrorSetting[2]; break;
      case 8: $newMirrorSetting[3] = $mirrorSetting[3]; break;
      case 9: $newMirrorSetting[4] = $mirrorSetting[4]; break;
      default: break;
    }

    $nkPoseStackPromptPoseSettings[$nodeIndex] = buildPoseSetting(
      $newPasteSetting, $newSelectSetting, $newMirrorSetting
    );
  }
  updateNodeList(true);
}

/*-
@param $pasteSetting <int[]>
@returns <>
*/
proc updatePasteSettingUI(int $pasteSetting[]) {
  // print("updatePasteSettingUI\n"); // debug
  // print("  pasteSetting:{" + intArrayToString($pasteSetting, ", ") + "}\n"); // debug
  global int $nkPoseStackPromptNeedUpdateInitSetting[];
  string $pasteAttributesCheckBoxGrp = nkUIGetControl("layoutDialog", "pasteAttributesCheckBoxGrp");
  int $isSelected = `size $pasteSetting`;
  checkBoxGrpSetEnable($isSelected, $pasteAttributesCheckBoxGrp);
  if ($isSelected) {
    checkBoxGrpSetValues($pasteSetting, $pasteAttributesCheckBoxGrp);
  }
  if (!$nkPoseStackPromptNeedUpdateInitSetting[0] && $isSelected) {
    $nkPoseStackPromptNeedUpdateInitSetting[0] = true;
  }
}

/*-
@param $selectSetting <string[]>
@returns <>
*/
proc updateSelectSettingUI(string $selectSetting[]) {
  // print("updateSelectSettingUI\n"); // debug
  // print("  selectSetting:{" + stringArrayToString($selectSetting, ", ") + "}\n"); // debug
  global int $nkPoseStackPromptNeedUpdateInitSetting[];
  string $selectPatternAText = nkUIGetControl("layoutDialog", "selectPatternAText");
  string $selectPatternBText = nkUIGetControl("layoutDialog", "selectPatternBText");
  int $isSelected = `size $selectSetting`;
  textFieldGrpSetEnable($isSelected, $selectPatternAText);
  textFieldGrpSetEnable($isSelected, $selectPatternBText);
  if ($isSelected) {
    textFieldGrpSetText($selectSetting[0], $selectPatternAText);
    textFieldGrpSetText($selectSetting[1], $selectPatternBText);
  }
  if (!$nkPoseStackPromptNeedUpdateInitSetting[1] && $isSelected) {
    $nkPoseStackPromptNeedUpdateInitSetting[1] = true;
  }
}

/*-
@param $mirrorSetting <int[]>
@returns <>
*/
proc updateMirrorSettingUI(int $mirrorSetting[]) {
  // print("updateMirrorSettingUI\n"); // debug
  // print("  mirrorSetting:{" + intArrayToString($mirrorSetting, ", ") + "}\n"); // debug
  global int $nkPoseStackPromptNeedUpdateInitSetting[];
  string $mirrorAxisRadioButton = nkUIGetControl("layoutDialog", "mirrorAxisRadioButton");
  string $primaryAxisRadioButton = nkUIGetControl("layoutDialog", "primaryAxisRadioButton");
  string $secondaryAxisRadioButton = nkUIGetControl("layoutDialog", "secondaryAxisRadioButton");
  string $invertAxisCheckBoxGrp = nkUIGetControl("layoutDialog", "invertAxisCheckBoxGrp");
  int $isSelected = `size $mirrorSetting`;
  radioButtonGrpSetEnable($isSelected, $mirrorAxisRadioButton);
  radioButtonGrpSetEnable($isSelected, $primaryAxisRadioButton);
  radioButtonGrpSetEnable($isSelected, $secondaryAxisRadioButton);
  checkBoxGrpSetEnable($isSelected, $invertAxisCheckBoxGrp);
  if ($isSelected) {
    radioButtonGrpSetSelect($mirrorSetting[0], $mirrorAxisRadioButton);
    radioButtonGrpSetSelect($mirrorSetting[1], $primaryAxisRadioButton);
    radioButtonGrpSetSelect($mirrorSetting[2], $secondaryAxisRadioButton);
    checkBoxGrpSetValues(
      {$mirrorSetting[3], $mirrorSetting[4]},
      $invertAxisCheckBoxGrp
    );
  }
  if (!$nkPoseStackPromptNeedUpdateInitSetting[2] && $isSelected) {
    $nkPoseStackPromptNeedUpdateInitSetting[2] = true;
  }
}

/*-
@returns <>
*/
proc poseNameTextChanged() {
  // print("poseNameTextChanged\n"); // debug
  global string $nkPoseStackPromptPoseName;
  string $poseNameText = nkUIGetControl("layoutDialog", "poseNameText");
  string $currentText = textFieldGrpGetText($poseNameText);
  string $newText = sanitizeText($currentText);
  int $numNewText = `size $newText`;
  if ($newText != $currentText || !$numNewText) {
    int $position = textFieldGrpGetInsertionPosition($poseNameText);
    // print("  position:" + $position + "\n"); // debug
    if (!$numNewText) {
      $newText = "_";
      $position = -1;
    }
    textFieldGrpSetText($newText, $poseNameText);
    textFieldGrpSetInsertionPosition($position, $poseNameText);
  }
  $nkPoseStackPromptPoseName = $newText;
}

/*-
@returns <>
*/
proc nodeListSelected() {
  // print("nodeListSelected\n"); // debug
  global string $nkPoseStackPromptPoseSettings[];
  global int $nkPoseStackPromptIsUIUpdating;

  string $nodeList = nkUIGetControl("layoutDialog", "nodeList");
  int $nodeIndices[] = textScrollListGetSelectIndexedItems($nodeList);
  // print("  nodeIndices:{" + intArrayToString($nodeIndices, ", ") + "}\n"); // debug
  int $pasteSetting[];
  string $selectSetting[];
  int $mirrorSetting[];
  if (`size $nodeIndices`) {
    string $poseSetting = $nkPoseStackPromptPoseSettings[$nodeIndices[0]];
    $pasteSetting = extractPasteSetting($poseSetting);
    $selectSetting = extractSelectSetting($poseSetting);
    $mirrorSetting = extractMirrorSetting($poseSetting);
  }
  // print("  pasteSetting :{" + intArrayToString($pasteSetting, ", ") + "}\n"); // debug
  // print("  selectSetting:{" + stringArrayToString($selectSetting, ", ") + "}\n"); // debug
  // print("  mirrorSetting:{" + intArrayToString($mirrorSetting, ", ") + "}\n"); // debug

  $nkPoseStackPromptIsUIUpdating = true;
  updatePasteSettingUI($pasteSetting);
  updateSelectSettingUI($selectSetting);
  updateMirrorSettingUI($mirrorSetting);
  $nkPoseStackPromptIsUIUpdating = false;
}

/*-
@returns <>
*/
proc pasteAttributesCheckBoxGrp1Changed() {
  // print("pasteAttributesCheckBoxGrp1Changed\n"); // debug
  updatePoseSettings(0);
}

/*-
@returns <>
*/
proc pasteAttributesCheckBoxGrp2Changed() {
  // print("pasteAttributesCheckBoxGrp2Changed\n"); // debug
  updatePoseSettings(1);
}

/*-
@returns <>
*/
proc pasteAttributesCheckBoxGrp3Changed() {
  // print("pasteAttributesCheckBoxGrp3Changed\n"); // debug
  updatePoseSettings(2);
}

/*-
@returns <>
*/
proc selectPatternATextChanged() {
  // print("selectPatternATextChanged\n"); // debug
  string $selectPatternAText = nkUIGetControl("layoutDialog", "selectPatternAText");
  string $currentText = textFieldGrpGetText($selectPatternAText);
  string $newText = sanitizeText($currentText);
  int $numNewText = `size $newText`;
  if ($newText != $currentText || !$numNewText) {
    int $position = textFieldGrpGetInsertionPosition($selectPatternAText);
    // print("  position:" + $position + "\n"); // debug
    if (!$numNewText) {
      $newText = "_";
      $position = -1;
    }
    textFieldGrpSetText($newText, $selectPatternAText);
    textFieldGrpSetInsertionPosition($position, $selectPatternAText);
  }
  updatePoseSettings(3);
}

/*-
@returns <>
*/
proc selectPatternBTextChanged() {
  // print("selectPatternBTextChanged\n"); // debug
  string $selectPatternBText = nkUIGetControl("layoutDialog", "selectPatternBText");
  string $currentText = textFieldGrpGetText($selectPatternBText);
  string $newText = sanitizeText($currentText);
  int $numNewText = `size $newText`;
  if ($newText != $currentText || !$numNewText) {
    int $position = textFieldGrpGetInsertionPosition($selectPatternBText);
    // print("  position:" + $position + "\n"); // debug
    if (!$numNewText) {
      $newText = "_";
      $position = -1;
    }
    textFieldGrpSetText($newText, $selectPatternBText);
    textFieldGrpSetInsertionPosition($position, $selectPatternBText);
  }
  updatePoseSettings(4);
}

/*-
@returns <>
*/
proc mirrorAxisRadioButtonOn() {
  // print("mirrorAxisRadioButtonOn\n"); // debug
  updatePoseSettings(5);
}

/*-
@returns <>
*/
proc primaryAxisRadioButtonOn() {
  // print("primaryAxisRadioButtonOn\n"); // debug
  string $primaryAxisRadioButton = nkUIGetControl("layoutDialog", "primaryAxisRadioButton");
  string $secondaryAxisRadioButton = nkUIGetControl("layoutDialog", "secondaryAxisRadioButton");
  int $primaryAxisSelect = radioButtonGrpGetSelect($primaryAxisRadioButton);
  int $secondaryAxisSelect = radioButtonGrpGetSelect($secondaryAxisRadioButton);
  updatePoseSettings(6);
  if ($primaryAxisSelect == $secondaryAxisSelect) {
    $secondaryAxisSelect = $primaryAxisSelect + 1;
    if ($secondaryAxisSelect > 2) {
      $secondaryAxisSelect = 0;
    }
    radioButtonGrpSetSelect($secondaryAxisSelect, $secondaryAxisRadioButton);
    // ラジオボタンはコマンド操作でイベントが発生しないのでこちら側で更新する
    updatePoseSettings(7);
  }
}

/*-
@returns <>
*/
proc secondaryAxisRadioButtonOn() {
  // print("secondaryAxisRadioButtonOn\n"); // debug
  string $primaryAxisRadioButton = nkUIGetControl("layoutDialog", "primaryAxisRadioButton");
  string $secondaryAxisRadioButton = nkUIGetControl("layoutDialog", "secondaryAxisRadioButton");
  int $primaryAxisSelect = radioButtonGrpGetSelect($primaryAxisRadioButton);
  int $secondaryAxisSelect = radioButtonGrpGetSelect($secondaryAxisRadioButton);
  updatePoseSettings(7);
  if ($primaryAxisSelect == $secondaryAxisSelect) {
    $primaryAxisSelect = $secondaryAxisSelect - 1;
    if ($primaryAxisSelect < 0) {
      $primaryAxisSelect = 2;
    }
    radioButtonGrpSetSelect($primaryAxisSelect, $primaryAxisRadioButton);
    // ラジオボタンはコマンド操作でイベントが発生しないのでこちら側で更新する
    updatePoseSettings(6);
  }
}

/*-
@returns <>
*/
proc invertAxisCheckBoxGrp1Changed() {
  // print("invertAxisCheckBoxGrp1Changed\n"); // debug
  updatePoseSettings(8);
}

/*-
@returns <>
*/
proc invertAxisCheckBoxGrp2Changed() {
  // print("invertAxisCheckBoxGrp2Changed\n"); // debug
  updatePoseSettings(9);
}

/*-
@returns <>
*/
proc okButtonPressed() {
  // print("okButtonPressed\n"); // debug
  global int $nkPoseStackInitPasteSetting[];
  global string $nkPoseStackInitSelectSetting[];
  global int $nkPoseStackInitMirrorSetting[];
  global int $nkPoseStackPromptNeedUpdateInitSetting[];
  // print("  nkPoseStackPromptNeedUpdateInitSetting:{" + intArrayToString($nkPoseStackPromptNeedUpdateInitSetting, ", ") + "}\n"); // debug

  if ($nkPoseStackPromptNeedUpdateInitSetting[0]) {
    string $pasteAttributesCheckBoxGrp = nkUIGetControl("layoutDialog", "pasteAttributesCheckBoxGrp");
    $nkPoseStackInitPasteSetting = checkBoxGrpGetValues(
      3, $pasteAttributesCheckBoxGrp
    );
  }

  if ($nkPoseStackPromptNeedUpdateInitSetting[1]) {
    string $selectPatternAText = nkUIGetControl("layoutDialog", "selectPatternAText");
    string $selectPatternBText = nkUIGetControl("layoutDialog", "selectPatternBText");
    $nkPoseStackInitSelectSetting = {
      sanitizeText(textFieldGrpGetText($selectPatternAText)),
      sanitizeText(textFieldGrpGetText($selectPatternBText))
    };
  }

  if ($nkPoseStackPromptNeedUpdateInitSetting[2]) {
    string $mirrorAxisRadioButton = nkUIGetControl("layoutDialog", "mirrorAxisRadioButton");
    string $primaryAxisRadioButton = nkUIGetControl("layoutDialog", "primaryAxisRadioButton");
    string $secondaryAxisRadioButton = nkUIGetControl("layoutDialog", "secondaryAxisRadioButton");
    string $invertAxisCheckBoxGrp = nkUIGetControl("layoutDialog", "invertAxisCheckBoxGrp");
    int $invertAxisValues[] = checkBoxGrpGetValues(2, $invertAxisCheckBoxGrp);
    $nkPoseStackInitMirrorSetting = {
      radioButtonGrpGetSelect($mirrorAxisRadioButton),
      radioButtonGrpGetSelect($primaryAxisRadioButton),
      radioButtonGrpGetSelect($secondaryAxisRadioButton),
      $invertAxisValues[0],
      $invertAxisValues[1]
    };
  }

  layoutDialog -dis "OK";
}

/*-
@returns <>
*/
proc cancelButtonPressed() {
  // print("cancelButtonPressed\n"); // debug
  layoutDialog -dis "Cancel";
}

/*-
@returns <>
*/
proc updatePoseList() {
  // print("updatePoseList\n"); // debug
  string $poseList = nkUIGetControl("nkPoseStackWindow", "poseList");
  int $poseIndex = textScrollListGetSelectIndexedItem($poseList);
  // print("  poseIndex:" + $poseIndex + "\n"); // debug
  string $poseNames[] = getPoseNames();
  int $numPoseNames = `size $poseNames`;
  textScrollListRemoveAll($poseList);
  for ($poseName in $poseNames) {
    textScrollListAppend($poseName, $poseList);
  }
}

/*-
@param $n <string>
@returns <string>
*/
proc string makePoseName(string $n) {
  string $poseName = $n;
  string $buffer[];
  int $depth = `tokenize $n "|" $buffer`;
  if ($depth > 0) {
    $poseName = (($depth > 1) ? $buffer[0] + "-" : "") + $buffer[$depth - 1];
  }
  return $poseName;
}

/*-
@returns <>
*/
proc nkPoseStackWindowClosed() {
  // print("nkPoseStackWindowClosed\n"); // debug
  nkUIRemoveControls("nkPoseStackWindow", "");
}

/*-
@returns <>
*/
proc openMenuItemSelected() {
  print("openMenuItemSelected\n");
}

/*-
@returns <>
*/
proc importMenuItemSelected() {
  print("importMenuItemSelected\n");
}

/*-
@returns <>
*/
proc saveAsMenuItemSelected() {
  print("saveAsMenuItemSelected\n");
}

/*-
@returns <>
*/
proc quitMenuItemSelected() {
  // print("quitMenuItemSelected\n"); // debug
  string $nkPoseStackWindow = nkUIGetControl("nkPoseStackWindow", "nkPoseStackWindow");
  // print("  nkPoseStackWindow:" + $nkPoseStackWindow + "\n"); // debug
  int $exists = workspaceControlGetExists($nkPoseStackWindow);
  // print("    exists   :" + $exists + "\n"); // debug
  if (!$exists) return;
  int $isVisible = workspaceControlGetVisible($nkPoseStackWindow);
  // print("    isVisible:" + $isVisible + "\n"); // debug
  if (!$isVisible) return;
  workspaceControlClose($nkPoseStackWindow);
}

/*-
@returns <>
*/
proc aboutMenuItemSelected() {
  // print("aboutMenuItemSelected\n"); // debug
  confirmDialog
      -b "OK"
      -db "OK"
      -m (
        "nkPoseStack " + (nkPoseStackVersion())
        + "\n\n(c) 2023 imaoki | MIT License | https://github.com/imaoki"
      )
      -p (getParentWindow())
      -t "About nkPoseStack";
}

/*-
@returns <>
*/
proc addPoseButtonPressed() {
  // print("addPoseButtonPressed\n"); // debug
  global int $nkPoseStackInitPasteSetting[];
  global string $nkPoseStackInitSelectSetting[];
  global int $nkPoseStackInitMirrorSetting[];
  // print("  nkPoseStackInitPasteSetting :{" + intArrayToString($nkPoseStackInitPasteSetting, ", ") + "}\n"); // debug
  // print("  nkPoseStackInitSelectSetting:{" + stringArrayToString($nkPoseStackInitSelectSetting, ", ") + "}\n"); // debug
  // print("  nkPoseStackInitMirrorSetting:{" + intArrayToString($nkPoseStackInitMirrorSetting, ", ") + "}\n"); // debug

  global string $nkPoseStackPromptPoseName;
  global string $nkPoseStackPromptNodes[];
  global string $nkPoseStackPromptPoseSettings[];
  initializePromptVariables();

  string $nodes[] = getSelectedNodes();
  // print("  nodes:{\n    " + (stringArrayToString($nodes, ",\n    ")) + "\n  }\n"); // debug
  int $numNodes = `size $nodes`;
  if (!$numNodes) return;

  $nkPoseStackPromptPoseName = makePoseName($nodes[0]);
  $nkPoseStackPromptNodes = $nodes;
  for ($i = 0; $i < $numNodes; $i++) {
    $nkPoseStackPromptPoseSettings[$i] = buildPoseSetting(
      $nkPoseStackInitPasteSetting,
      $nkPoseStackInitSelectSetting,
      $nkPoseStackInitMirrorSetting
    );
  }
  // print("  nkPoseStackPromptPoseName    :" + $nkPoseStackPromptPoseName + "\n"); // debug
  // print("  nkPoseStackPromptNodes       :{\n    " + stringArrayToString($nkPoseStackPromptNodes, ",\n    ") + "\n  }\n"); // debug
  // print("  nkPoseStackPromptPoseSettings:{\n    " + stringArrayToString($nkPoseStackPromptPoseSettings, ",\n    ") + "\n  }\n"); // debug

  string $result = `layoutDialog
      -p (getParentWindow())
      -t "Add Pose"
      -ui "buildPromptWindow"`;

  nkUIRemoveControls("layoutDialog", "");

  // print("  result:" + $result + "\n"); // debug
  // print("    nkPoseStackInitPasteSetting  :{" + intArrayToString($nkPoseStackInitPasteSetting, ", ") + "}\n"); // debug
  // print("    nkPoseStackInitSelectSetting :{" + stringArrayToString($nkPoseStackInitSelectSetting, ", ") + "}\n"); // debug
  // print("    nkPoseStackInitMirrorSetting :{" + intArrayToString($nkPoseStackInitMirrorSetting, ", ") + "}\n"); // debug
  // print("    nkPoseStackPromptPoseName    :" + $nkPoseStackPromptPoseName + "\n"); // debug
  // print("    nkPoseStackPromptNodes       :{\n      " + stringArrayToString($nkPoseStackPromptNodes, ",\n      ") + "\n    }\n"); // debug
  // print("    nkPoseStackPromptPoseSettings:{\n      " + stringArrayToString($nkPoseStackPromptPoseSettings, ",\n      ") + "\n    }\n"); // debug

  if ($result != "OK") return;

  int $numPoses = addPose(
    $nkPoseStackPromptPoseName,
    $nkPoseStackPromptNodes,
    $nkPoseStackPromptPoseSettings
  );

  updatePoseList();
  string $poseList = nkUIGetControl("nkPoseStackWindow", "poseList");
  textScrollListSetSelectIndexedItem($numPoses - 1, $poseList);
}

/*-
@returns <>
*/
proc deletePoseButtonPressed() {
  // print("deletePoseButtonPressed\n"); // debug
  string $poseList = nkUIGetControl("nkPoseStackWindow", "poseList");
  int $poseIndex = textScrollListGetSelectIndexedItem($poseList);
  // print("  poseIndex:" + $poseIndex + "\n"); // debug
  if ($poseIndex == -1) return;
  int $numPoses = deletePose($poseIndex);
  updatePoseList();
  if ($numPoses) {
    if ($poseIndex > $numPoses - 1) $poseIndex = $numPoses - 1;
    textScrollListSetSelectIndexedItem($poseIndex, $poseList);
  }
}

/*-
@returns <>
*/
proc editPoseButtonPressed() {
  // print("editPoseButtonPressed\n"); // debug
  global int $nkPoseStackInitPasteSetting[];
  global string $nkPoseStackInitSelectSetting[];
  global int $nkPoseStackInitMirrorSetting[];
  // print("  nkPoseStackInitPasteSetting :{" + intArrayToString($nkPoseStackInitPasteSetting, ", ") + "}\n"); // debug
  // print("  nkPoseStackInitSelectSetting:{" + stringArrayToString($nkPoseStackInitSelectSetting, ", ") + "}\n"); // debug
  // print("  nkPoseStackInitMirrorSetting:{" + intArrayToString($nkPoseStackInitMirrorSetting, ", ") + "}\n"); // debug

  global string $nkPoseStackPromptPoseName;
  global string $nkPoseStackPromptNodes[];
  global string $nkPoseStackPromptPoseSettings[];
  initializePromptVariables();

  string $poseList = nkUIGetControl("nkPoseStackWindow", "poseList");
  int $poseIndex = textScrollListGetSelectIndexedItem($poseList);
  // print("  poseIndex:" + $poseIndex + "\n"); // debug
  string $pose = getPose($poseIndex);
  if (!`size $pose`) return;

  $nkPoseStackPromptPoseName = extractPoseName($pose);

  string $poseTransforms[] = extractPoseTransforms($pose);
  // print("  poseTransforms:{\n    " + stringArrayToString($poseTransforms, ",\n    ") + "\n  }\n"); // debug
  if (!`size $poseTransforms`) return;

  for ($i = 0; $i < `size $poseTransforms`; $i++) {
    string $poseTransform = $poseTransforms[$i];
    // print("  poseTransform:" + $poseTransform + "\n"); // debug
    string $node = extractTransformNode($poseTransform);
    // print("    node       :" + $node + "\n"); // debug
    string $poseSetting = extractPoseSetting($poseTransform);
    // print("    poseSetting:" + $poseSetting + "\n"); // debug
    $nkPoseStackPromptNodes[$i] = $node;
    $nkPoseStackPromptPoseSettings[$i] = $poseSetting;
  }
  // print("  nkPoseStackPromptPoseName    :" + $nkPoseStackPromptPoseName + "\n"); // debug
  // print("  nkPoseStackPromptNodes       :{\n    " + stringArrayToString($nkPoseStackPromptNodes, ",\n    ") + "\n  }\n"); // debug
  // print("  nkPoseStackPromptPoseSettings:{\n    " + stringArrayToString($nkPoseStackPromptPoseSettings, ",\n    ") + "\n  }\n"); // debug

  string $result = `layoutDialog
      -p (getParentWindow())
      -t "Edit Pose"
      -ui "buildPromptWindow"`;

  nkUIRemoveControls("layoutDialog", "");

  // print("  result:" + $result + "\n"); // debug
  // print("    nkPoseStackInitPasteSetting  :{" + intArrayToString($nkPoseStackInitPasteSetting, ", ") + "}\n"); // debug
  // print("    nkPoseStackInitSelectSetting :{" + stringArrayToString($nkPoseStackInitSelectSetting, ", ") + "}\n"); // debug
  // print("    nkPoseStackInitMirrorSetting :{" + intArrayToString($nkPoseStackInitMirrorSetting, ", ") + "}\n"); // debug
  // print("    nkPoseStackPromptPoseName    :" + $nkPoseStackPromptPoseName + "\n"); // debug
  // print("    nkPoseStackPromptNodes       :{\n      " + stringArrayToString($nkPoseStackPromptNodes, ",\n      ") + "\n    }\n"); // debug
  // print("    nkPoseStackPromptPoseSettings:{\n      " + stringArrayToString($nkPoseStackPromptPoseSettings, ",\n      ") + "\n    }\n"); // debug

  if ($result != "OK") return;

  editPose(
    $poseIndex,
    $nkPoseStackPromptPoseName,
    $nkPoseStackPromptPoseSettings
  );

  updatePoseList();
  textScrollListSetSelectIndexedItem($poseIndex, $poseList);
}

/*-
@returns <>
*/
proc duplicatePoseButtonPressed() {
  // print("duplicatePoseButtonPressed\n"); // debug
  string $poseList = nkUIGetControl("nkPoseStackWindow", "poseList");
  int $poseIndex = textScrollListGetSelectIndexedItem($poseList);
  // print("  poseIndex:" + $poseIndex + "\n"); // debug
  if ($poseIndex == -1) return;
  int $newPoseIndex = duplicatePose($poseIndex);
  if ($poseIndex == $newPoseIndex) return;
  updatePoseList();
  textScrollListSetSelectIndexedItem($newPoseIndex, $poseList);
}

/*-
@returns <>
*/
proc moveUpPoseButtonPressed() {
  // print("moveUpPoseButtonPressed\n"); // debug
  string $poseList = nkUIGetControl("nkPoseStackWindow", "poseList");
  int $poseIndex = textScrollListGetSelectIndexedItem($poseList);
  // print("  poseIndex:" + $poseIndex + "\n"); // debug
  if ($poseIndex == -1) return;
  int $newPoseIndex = moveUpPose($poseIndex);
  if ($poseIndex == $newPoseIndex) return;
  updatePoseList();
  textScrollListSetSelectIndexedItem($newPoseIndex, $poseList);
}

/*-
@returns <>
*/
proc moveDownPoseButtonPressed() {
  // print("moveDownPoseButtonPressed\n"); // debug
  string $poseList = nkUIGetControl("nkPoseStackWindow", "poseList");
  int $poseIndex = textScrollListGetSelectIndexedItem($poseList);
  // print("  poseIndex:" + $poseIndex + "\n"); // debug
  if ($poseIndex == -1) return;
  int $newPoseIndex = moveDownPose($poseIndex);
  if ($poseIndex == $newPoseIndex) return;
  updatePoseList();
  textScrollListSetSelectIndexedItem($newPoseIndex, $poseList);
}

/*-
@returns <>
*/
proc poseListDoubleClicked() {
  // print("poseListDoubleClicked\n"); // debug
  editPoseButtonPressed();
}

/*-
@returns <>
*/
proc updatePoseButtonPressed() {
  // print("updatePoseButtonPressed\n"); // debug
  string $poseList = nkUIGetControl("nkPoseStackWindow", "poseList");
  int $poseIndex = textScrollListGetSelectIndexedItem($poseList);
  // print("  poseIndex:" + $poseIndex + "\n"); // debug
  if ($poseIndex == -1) return;
  updatePose($poseIndex);
}

/*-
@param $isOpposite <boolean>
@returns <>
*/
proc selectButtonPressed(int $isOpposite) {
  // print("selectButtonPressed\n"); // debug
  // print("  isOpposite:" + $isOpposite + "\n"); // debug
  string $poseList = nkUIGetControl("nkPoseStackWindow", "poseList");
  int $poseIndex = textScrollListGetSelectIndexedItem($poseList);
  // print("  poseIndex:" + $poseIndex + "\n"); // debug
  if ($poseIndex == -1) return;
  selectNodes($poseIndex, $isOpposite);
}

/*-
@param $isOpposite <boolean>
@returns <>
*/
proc pasteButtonPressed(int $isOpposite) {
  // print("pasteButtonPressed\n"); // debug
  // print("  isOpposite:" + $isOpposite + "\n"); // debug
  string $poseList = nkUIGetControl("nkPoseStackWindow", "poseList");
  int $poseIndex = textScrollListGetSelectIndexedItem($poseList);
  // print("  poseIndex:" + $poseIndex + "\n"); // debug
  if ($poseIndex == -1) return;
  string $nodes[] = getSelectedNodes();
  // print("  nodes:{\n    " + (stringArrayToString($nodes, ",\n    ")) + "\n  }\n"); // debug
  if (!`size $nodes`) return;
  pastePose($poseIndex, $isOpposite, $nodes);
}

/*-
@param $window <string>
@param $event <string>
@param $control <string>
@returns <>
*/
global proc nkPoseStackInvokeEvent(
  string $window,
  string $event,
  string $control
) {
  switch ($window) {
    case "layoutDialog":
      switch ($event) {
        case "c":
          switch ($control) {
            case "okButton": okButtonPressed(); break;
            case "cancelButton": cancelButtonPressed(); break;
            default: break;
          }
          break;
        case "cc1":
          switch ($control) {
            case "pasteAttributesCheckBoxGrp": pasteAttributesCheckBoxGrp1Changed(); break;
            case "invertAxisCheckBoxGrp": invertAxisCheckBoxGrp1Changed(); break;
            default: break;
          }
          break;
        case "cc2":
          switch ($control) {
            case "pasteAttributesCheckBoxGrp": pasteAttributesCheckBoxGrp2Changed(); break;
            case "invertAxisCheckBoxGrp": invertAxisCheckBoxGrp2Changed(); break;
            default: break;
          }
          break;
        case "cc3":
          switch ($control) {
            case "pasteAttributesCheckBoxGrp": pasteAttributesCheckBoxGrp3Changed(); break;
            default: break;
          }
          break;
        case "onc":
          switch ($control) {
            case "mirrorAxisRadioButton": mirrorAxisRadioButtonOn(); break;
            case "primaryAxisRadioButton": primaryAxisRadioButtonOn(); break;
            case "secondaryAxisRadioButton": secondaryAxisRadioButtonOn(); break;
            default: break;
          }
          break;
        case "sc":
          switch ($control) {
            case "nodeList": nodeListSelected(); break;
            default: break;
          }
          break;
        case "tcc":
          switch ($control) {
            case "poseNameText": poseNameTextChanged(); break;
            case "selectPatternAText": selectPatternATextChanged(); break;
            case "selectPatternBText": selectPatternBTextChanged(); break;
            default: break;
          }
          break;
        default: break;
      }
      break;
    case "nkPoseStackWindow":
      switch ($event) {
        case "c":
          switch ($control) {
            // case "openMenuItem": openMenuItemSelected(); break;
            // case "importMenuItem": importMenuItemSelected(); break;
            // case "saveAsMenuItem": saveAsMenuItemSelected(); break;
            case "quitMenuItem": quitMenuItemSelected(); break;
            case "aboutMenuItem": aboutMenuItemSelected(); break;
            case "addPoseButton": addPoseButtonPressed(); break;
            case "deletePoseButton": deletePoseButtonPressed(); break;
            case "editPoseButton": editPoseButtonPressed(); break;
            case "duplicatePoseButton": duplicatePoseButtonPressed(); break;
            case "moveUpPoseButton": moveUpPoseButtonPressed(); break;
            case "moveDownPoseButton": moveDownPoseButtonPressed(); break;
            case "updatePoseButton": updatePoseButtonPressed(); break;
            case "selectSourceButton": selectButtonPressed(false); break;
            case "selectOppositeButton": selectButtonPressed(true); break;
            case "pasteSourceButton": pasteButtonPressed(false); break;
            case "pasteOppositeButton": pasteButtonPressed(true); break;
            default: break;
          }
          break;
        case "cc":
          switch ($control) {
            case "nkPoseStackWindow": nkPoseStackWindowClosed(); break;
            default: break;
          }
          break;
        case "dcc":
          switch ($control) {
            case "poseList": poseListDoubleClicked(); break;
            default: break;
          }
          break;
      }
      break;
    default: break;
  }
}

// -----------------------------------------------------------------------------
// ウィンドウ定義
// -----------------------------------------------------------------------------
/*-
@returns <>
*/
global proc buildPromptWindow() {
  // print("buildPromptWindow\n"); // debug
  global string $nkPoseStackPromptPoseName;

  string $outerForm = `setParent -q`;
    string $poseNameText = `textFieldGrp
        -adj 2
        -cat 2 "right" -5
        -cw2 60 10
        -l "Pose Name:"
        -tx $nkPoseStackPromptPoseName
        "poseNameText"`;
    string $nodeList = `textScrollList -ams true -h 18 "nodeList"`;
    string $poseSettingColumn = `columnLayout
        -adj true
        -rs 3
        "poseSettingColumn"`;
      string $pasteFrame = `frameLayout
          -l "Paste"
          -bgs true
          -cll false
          -mh 7
          -mw 7
          "pasteFrame"`;
        string $pasteColumn = `columnLayout
            -adj true
            -rs 3
            "pasteColumn"`;
          string $pasteAttributesCheckBoxGrp = `checkBoxGrp
              -cw4 80 60 60 60
              -en false
              -l "Attributes:"
              -la3 "T" "R" "S"
              -ncb 3
              "pasteAttributesCheckBoxGrp"`;
        setParent ..;
      setParent ..;
      string $selectFrame = `frameLayout
          -l "Select"
          -bgs true
          -cll false
          -mh 7
          -mw 7
          "selectFrame"`;
        string $selectColumn = `columnLayout
            -adj true
            -rs 3
            "selectColumn"`;
          string $selectTextForm = `formLayout "selectTextForm"`;
            string $selectPatternAText = `textFieldGrp
                -adj 2
                -cat 2 "right" -5
                -cw2 16 10
                -en false
                -l "A:"
                "selectPatternAText"`;
            string $selectPatternBText = `textFieldGrp
                -adj 2
                -cat 2 "right" -5
                -cw2 16 10
                -en false
                -l "B:"
                "selectPatternBText"`;
          setParent ..;
        setParent ..;
      setParent ..;
      string $mirrorFrame = `frameLayout
          -l "Mirror"
          -bgs true
          -cll false
          -mh 7
          -mw 7
          "mirrorFrame"`;
        string $mirrorColumn = `columnLayout
            -adj true
            -rs 3
            "mirrorColumn"`;
          string $mirrorAxisRadioButton = `radioButtonGrp
              // -bgc 0.25 0.25 0.5
              -cw4 80 60 60 60
              -en false
              -l "Mirror Axis:"
              -la3 "X" "Y" "Z"
              -nrb 3
              "mirrorAxisRadioButton"`;
          string $primaryAxisRadioButton = `radioButtonGrp
              // -bgc 0.25 0.25 0.5
              -cw4 80 60 60 60
              -en false
              -l "Primary Axis:"
              -la3 "X" "Y" "Z"
              -nrb 3
              "primaryAxisRadioButton"`;
          string $secondaryAxisRadioButton = `radioButtonGrp
              // -bgc 0.25 0.25 0.5
              -cw4 80 60 60 60
              -en false
              -l "Secondary Axis:"
              -la3 "X" "Y" "Z"
              -nrb 3
              "secondaryAxisRadioButton"`;
          string $invertAxisCheckBoxGrp = `checkBoxGrp
              -cw3 80 60 60
              -en false
              -l "Invert Axis:"
              -la2 "Primary" "Secondary"
              -ncb 2
              "invertAxisCheckBoxGrp"`;
        setParent ..;
      setParent ..;
    setParent ..;

    string $buttonsForm = `formLayout "buttonsForm"`;
      string $okButton = `button -h 26 -l "OK" "okButton"`;
      string $cancelButton = `button -h 26 -l "Cancel" "cancelButton"`;
    setParent ..;

  formLayout -e
      -af $poseNameText "top" 5
      -af $poseNameText "left" 5
      -af $poseNameText "right" 5
      -ac $nodeList "top" 3 $poseNameText
      -af $nodeList "left" 3
      -af $nodeList "right" 3
      -ac $nodeList "bottom" 3 $poseSettingColumn
      -af $poseSettingColumn "left" 3
      -af $poseSettingColumn "right" 3
      -ac $poseSettingColumn "bottom" 3 $buttonsForm
      -af $buttonsForm "left" 5
      -af $buttonsForm "right" 5
      -af $buttonsForm "bottom" 5
      $outerForm;

  formLayout -e
      -af $selectPatternAText "top" 0
      -af $selectPatternAText "left" 0
      -ap $selectPatternAText "right" 0 50
      -af $selectPatternBText "top" 0
      -ap $selectPatternBText "left" 0 50
      -af $selectPatternBText "right" 0
      $selectTextForm;

  formLayout -e
      -af $okButton "top" 2
      -af $okButton "left" 0
      -ap $okButton "right" 2 50
      -af $okButton "bottom" 0
      -af $cancelButton "top" 2
      -ap $cancelButton "left" 2 50
      -af $cancelButton "right" 0
      -af $cancelButton "bottom" 0
      $buttonsForm;

  textFieldGrp -e -tcc "nkPoseStackInvokeEvent(\"layoutDialog\", \"tcc\", \"poseNameText\");" $poseNameText;
  textScrollList -e -sc "nkPoseStackInvokeEvent(\"layoutDialog\", \"sc\", \"nodeList\");" $nodeList;
  checkBoxGrp -e -cc1 "nkPoseStackInvokeEvent(\"layoutDialog\", \"cc1\", \"pasteAttributesCheckBoxGrp\");" $pasteAttributesCheckBoxGrp;
  checkBoxGrp -e -cc2 "nkPoseStackInvokeEvent(\"layoutDialog\", \"cc2\", \"pasteAttributesCheckBoxGrp\");" $pasteAttributesCheckBoxGrp;
  checkBoxGrp -e -cc3 "nkPoseStackInvokeEvent(\"layoutDialog\", \"cc3\", \"pasteAttributesCheckBoxGrp\");" $pasteAttributesCheckBoxGrp;
  textFieldGrp -e -tcc "nkPoseStackInvokeEvent(\"layoutDialog\", \"tcc\", \"selectPatternAText\");" $selectPatternAText;
  textFieldGrp -e -tcc "nkPoseStackInvokeEvent(\"layoutDialog\", \"tcc\", \"selectPatternBText\");" $selectPatternBText;
  radioButtonGrp -e -onc "nkPoseStackInvokeEvent(\"layoutDialog\", \"onc\", \"mirrorAxisRadioButton\");" $mirrorAxisRadioButton;
  radioButtonGrp -e -onc "nkPoseStackInvokeEvent(\"layoutDialog\", \"onc\", \"primaryAxisRadioButton\");" $primaryAxisRadioButton;
  radioButtonGrp -e -onc "nkPoseStackInvokeEvent(\"layoutDialog\", \"onc\", \"secondaryAxisRadioButton\");" $secondaryAxisRadioButton;
  checkBoxGrp -e -cc1 "nkPoseStackInvokeEvent(\"layoutDialog\", \"cc1\", \"invertAxisCheckBoxGrp\");" $invertAxisCheckBoxGrp;
  checkBoxGrp -e -cc2 "nkPoseStackInvokeEvent(\"layoutDialog\", \"cc2\", \"invertAxisCheckBoxGrp\");" $invertAxisCheckBoxGrp;
  button -e -c "nkPoseStackInvokeEvent(\"layoutDialog\", \"c\", \"okButton\");" $okButton;
  button -e -c "nkPoseStackInvokeEvent(\"layoutDialog\", \"c\", \"cancelButton\");" $cancelButton;

  // print("// " + $outerForm + "\n"); // debug
  // print("// " + $poseNameText + "\n"); // debug
  // print("// " + $nodeList + "\n"); // debug
  // print("// " + $poseSettingColumn + "\n"); // debug
  // print("// " + $pasteFrame + "\n"); // debug
  // print("// " + $pasteColumn + "\n"); // debug
  // print("// " + $pasteAttributesCheckBoxGrp + "\n"); // debug
  // print("// " + $selectFrame + "\n"); // debug
  // print("// " + $selectColumn + "\n"); // debug
  // print("// " + $selectTextForm + "\n"); // debug
  // print("// " + $selectPatternAText + "\n"); // debug
  // print("// " + $selectPatternBText + "\n"); // debug
  // print("// " + $mirrorFrame + "\n"); // debug
  // print("// " + $mirrorColumn + "\n"); // debug
  // print("// " + $mirrorAxisRadioButton + "\n"); // debug
  // print("// " + $primaryAxisRadioButton + "\n"); // debug
  // print("// " + $secondaryAxisRadioButton + "\n"); // debug
  // print("// " + $invertAxisCheckBoxGrp + "\n"); // debug
  // print("// " + $buttonsForm + "\n"); // debug
  // print("// " + $okButton + "\n"); // debug
  // print("// " + $cancelButton + "\n"); // debug

  nkUIAppendControls({
    $poseNameText,
    $nodeList,
    $pasteAttributesCheckBoxGrp,
    $selectPatternAText,
    $selectPatternBText,
    $mirrorAxisRadioButton,
    $primaryAxisRadioButton,
    $secondaryAxisRadioButton,
    $invertAxisCheckBoxGrp
  });

  updateNodeList(false);
}

/*-
@returns <>
*/
global proc nkPoseStackBuildMainWindow() {
  // print("nkPoseStackBuildMainWindow\n"); // debug

  string $sourceDirname = getSourceDirname("nkPoseStack");
  string $iconDir;
  if (`size $sourceDirname`) $iconDir = $sourceDirname + "/nkPoseStackIcons/";
  string $addIcon = $iconDir + "addPose.png";
  string $deleteIcon = $iconDir + "deletePose.png";
  string $editIcon = $iconDir + "editPose.png";
  string $duplicateIcon = $iconDir + "duplicatePose.png";
  string $moveUpIcon = $iconDir + "moveUpPose.png";
  string $moveDownIcon = $iconDir + "moveDownPose.png";
  string $selectSourceIcon = $iconDir + "selectSource.png";
  string $selectOppositeIcon = $iconDir + "selectOpposite.png";
  string $pasteSourceIcon = $iconDir + "pasteSource.png";
  string $pasteOppositeIcon = $iconDir + "pasteOpposite.png";
  string $updateIcon = $iconDir + "updatePose.png";
  // print("  addIcon           :" + $addIcon + "\n"); // debug
  // print("  deleteIcon        :" + $deleteIcon + "\n"); // debug
  // print("  editIcon          :" + $editIcon + "\n"); // debug
  // print("  duplicateIcon     :" + $duplicateIcon + "\n"); // debug
  // print("  moveUpIcon        :" + $moveUpIcon + "\n"); // debug
  // print("  moveDownIcon      :" + $moveDownIcon + "\n"); // debug
  // print("  selectSourceIcon  :" + $selectSourceIcon + "\n"); // debug
  // print("  selectOppositeIcon:" + $selectOppositeIcon + "\n"); // debug
  // print("  pasteSourceIcon   :" + $pasteSourceIcon + "\n"); // debug
  // print("  pasteOppositeIcon :" + $pasteOppositeIcon + "\n"); // debug
  // print("  updateIcon        :" + $updateIcon + "\n"); // debug

  string $nkPoseStackWindow = `setParent -q`;
  // print("  nkPoseStackWindow:" + $nkPoseStackWindow + "\n"); // debug
    string $outerForm = `formLayout "outerForm"`;
      string $menuBar = `menuBarLayout
          // -bgc 0.25 0.5 0.25
          "menuBar"`;
        string $fileMenu = `menu
            -l "File"
            "fileMenu"`;
          // string $openMenuItem = `menuItem
          //     -l "Open"
          //     "openMenuItem"`;
          // string $importMenuItem = `menuItem
          //     -l "Import"
          //     "importMenuItem"`;
          // menuItem -d true;
          // string $saveAsMenuItem = `menuItem
          //     -l "Save As"
          //     "saveAsMenuItem"`;
          // menuItem -d true;
          string $quitMenuItem = `menuItem
              -l "Quit"
              "quitMenuItem"`;
        string $helpMenu = `menu
            -hm true
            -l "Help"
            "helpMenu"`;
          string $aboutMenuItem = `menuItem
              -l "About nkPoseStack"
              "aboutMenuItem"`;
      setParent ..;

      string $poseManipRow = `rowLayout
          // -bgc 0.5 0.25 0.25
          -nc 7
          -adj 5
          "poseManipRow"`;
        string $addPoseButton = `iconTextButton
            -ann "Add"
            // -bgc 0.25 0.5 0.5
            -i $addIcon
            -w 23
            "addPoseButton"`;
        string $deletePoseButton = `iconTextButton
            -ann "Delete"
            // -bgc 0.25 0.5 0.5
            -i $deleteIcon
            -w 23
            "deletePoseButton"`;
        string $editPoseButton = `iconTextButton
            -ann "Edit"
            // -bgc 0.25 0.5 0.5
            -i $editIcon
            -w 23
            "editPoseButton"`;
        string $duplicatePoseButton = `iconTextButton
            -ann "Duplicate"
            // -bgc 0.25 0.5 0.5
            -i $duplicateIcon
            -w 23
            "duplicatePoseButton"`;
        separator -st "none" -vis false;
        string $moveUpPoseButton = `iconTextButton
            -ann "Move Up"
            // -bgc 0.25 0.5 0.5
            -i $moveUpIcon
            -w 23
            "moveUpPoseButton"`;
        string $moveDownPoseButton = `iconTextButton
            -ann "Move Down"
            // -bgc 0.25 0.5 0.5
            -i $moveDownIcon
            -w 23
            "moveDownPoseButton"`;
      setParent ..;

      string $poseList = `textScrollList -ams false -h 18 "poseList"`;

      string $nodeManipRow = `rowLayout
          // -bgc 0.5 0.25 0.25
          -nc 7
          -adj 2
          "nodeManipRow"`;
        string $updatePoseButton = `iconTextButton
            -ann "Update"
            // -bgc 0.25 0.5 0.5
            -i $updateIcon
            -w 23
            "updatePoseButton"`;
        separator -st "none" -vis false;
        string $selectSourceButton = `iconTextButton
            -ann "Select Source"
            // -bgc 0.25 0.5 0.5
            -i $selectSourceIcon
            -w 23
            "selectSourceButton"`;
        string $selectOppositeButton = `iconTextButton
            -ann "Select Opposite"
            // -bgc 0.25 0.5 0.5
            -i $selectOppositeIcon
            -w 23
            "selectOppositeButton"`;
        separator -st "none" -vis false -w 23;
        string $pasteSourceButton = `iconTextButton
            -ann "As Is Paste"
            // -bgc 0.25 0.5 0.5
            -i $pasteSourceIcon
            -w 23
            "pasteSourceButton"`;
        string $pasteOppositeButton = `iconTextButton
            -ann "Mirror Paste"
            // -bgc 0.25 0.5 0.5
            -i $pasteOppositeIcon
            -w 23
            "pasteOppositeButton"`;
      setParent ..;
    setParent ..;

  formLayout -e
      -af $menuBar "top" 0
      -af $menuBar "left" 0
      -af $menuBar "right" 0
      -ac $poseManipRow "top" 0 $menuBar
      -af $poseManipRow "left" 5
      -af $poseManipRow "right" 5
      -ac $poseList "top" 5 $poseManipRow
      -af $poseList "left" 5
      -af $poseList "right" 5
      -ac $poseList "bottom" 5 $nodeManipRow
      -af $nodeManipRow "left" 5
      -af $nodeManipRow "right" 5
      -af $nodeManipRow "bottom" 5
      $outerForm;

  workspaceControl -e -cc "catchQuiet (nkPoseStackInvokeEvent(\"nkPoseStackWindow\", \"cc\", \"nkPoseStackWindow\"));" $nkPoseStackWindow;
  // menuItem -e -c "nkPoseStackInvokeEvent(\"nkPoseStackWindow\", \"c\", \"openMenuItem\");" $openMenuItem;
  // menuItem -e -c "nkPoseStackInvokeEvent(\"nkPoseStackWindow\", \"c\", \"importMenuItem\");" $importMenuItem;
  // menuItem -e -c "nkPoseStackInvokeEvent(\"nkPoseStackWindow\", \"c\", \"saveAsMenuItem\");" $saveAsMenuItem;
  menuItem -e -c "nkPoseStackInvokeEvent(\"nkPoseStackWindow\", \"c\", \"quitMenuItem\");" $quitMenuItem;
  menuItem -e -c "nkPoseStackInvokeEvent(\"nkPoseStackWindow\", \"c\", \"aboutMenuItem\");" $aboutMenuItem;
  iconTextButton -e -c "nkPoseStackInvokeEvent(\"nkPoseStackWindow\", \"c\", \"addPoseButton\");" $addPoseButton;
  iconTextButton -e -c "nkPoseStackInvokeEvent(\"nkPoseStackWindow\", \"c\", \"deletePoseButton\");" $deletePoseButton;
  iconTextButton -e -c "nkPoseStackInvokeEvent(\"nkPoseStackWindow\", \"c\", \"editPoseButton\");" $editPoseButton;
  iconTextButton -e -c "nkPoseStackInvokeEvent(\"nkPoseStackWindow\", \"c\", \"duplicatePoseButton\");" $duplicatePoseButton;
  iconTextButton -e -c "nkPoseStackInvokeEvent(\"nkPoseStackWindow\", \"c\", \"moveUpPoseButton\");" $moveUpPoseButton;
  iconTextButton -e -c "nkPoseStackInvokeEvent(\"nkPoseStackWindow\", \"c\", \"moveDownPoseButton\");" $moveDownPoseButton;
  textScrollList -e -dcc "nkPoseStackInvokeEvent(\"nkPoseStackWindow\", \"dcc\", \"poseList\");" $poseList;
  iconTextButton -e -c "nkPoseStackInvokeEvent(\"nkPoseStackWindow\", \"c\", \"updatePoseButton\");" $updatePoseButton;
  iconTextButton -e -c "nkPoseStackInvokeEvent(\"nkPoseStackWindow\", \"c\", \"selectSourceButton\");" $selectSourceButton;
  iconTextButton -e -c "nkPoseStackInvokeEvent(\"nkPoseStackWindow\", \"c\", \"selectOppositeButton\");" $selectOppositeButton;
  iconTextButton -e -c "nkPoseStackInvokeEvent(\"nkPoseStackWindow\", \"c\", \"pasteSourceButton\");" $pasteSourceButton;
  iconTextButton -e -c "nkPoseStackInvokeEvent(\"nkPoseStackWindow\", \"c\", \"pasteOppositeButton\");" $pasteOppositeButton;

  // print("  " + $nkPoseStackWindow + "\n"); // debug
  // print("  " + $outerForm + "\n"); // debug
  // print("  " + $menuBar + "\n"); // debug
  // print("  " + $fileMenu + "\n"); // debug
  // // print("  " + $openMenuItem + "\n"); // debug
  // // print("  " + $importMenuItem + "\n"); // debug
  // // print("  " + $saveAsMenuItem + "\n"); // debug
  // print("  " + $quitMenuItem + "\n"); // debug
  // print("  " + $helpMenu + "\n"); // debug
  // print("  " + $aboutMenuItem + "\n"); // debug
  // print("  " + $poseManipRow + "\n"); // debug
  // print("  " + $addPoseButton + "\n"); // debug
  // print("  " + $deletePoseButton + "\n"); // debug
  // print("  " + $editPoseButton + "\n"); // debug
  // print("  " + $duplicatePoseButton + "\n"); // debug
  // print("  " + $moveUpPoseButton + "\n"); // debug
  // print("  " + $moveDownPoseButton + "\n"); // debug
  // print("  " + $poseList + "\n"); // debug
  // print("  " + $nodeManipRow + "\n"); // debug
  // print("  " + $selectSourceButton + "\n"); // debug
  // print("  " + $selectOppositeButton + "\n"); // debug
  // print("  " + $pasteSourceButton + "\n"); // debug
  // print("  " + $pasteOppositeButton + "\n"); // debug
  // print("  " + $updatePoseButton + "\n"); // debug

  nkUIAppendControls({
    $nkPoseStackWindow,
    $poseList
  });

  updatePoseList();

  setFocus $nkPoseStackWindow;
  showWindow $nkPoseStackWindow;
}

/*-
@returns <>
*/
proc buildMainWorkspaceControl() {
  // print("buildMainWorkspaceControl\n"); // debug
  int $exists = workspaceControlGetExists("nkPoseStackWindow");
  // print("  exists:" + $exists + "\n"); // debug
  if (!$exists) {
    string $nkPoseStackWindow = `workspaceControl
        -dup false
        -fl true
        -l "nkPoseStack"
        -mw 260
        -rt false
        -ui "catchQuiet (nkPoseStackBuildMainWindow());"
        "nkPoseStackWindow"`;
  }
}

// -----------------------------------------------------------------------------
// システムイベント
// -----------------------------------------------------------------------------
/*-
@returns <>
*/
// proc registerScriptJob() {
// }

// -----------------------------------------------------------------------------
// メイン
// -----------------------------------------------------------------------------
/*-
@returns <>
*/
proc initialize() {
  initializeInitSettingVariables();
  buildMainWorkspaceControl();
  // registerScriptJob();
}

/*-
@returns <string>
*/
global proc string nkPoseStackVersion() {
  return "3.2.3";
}

/*-
@returns <>
*/
global proc nkPoseStack() {
  initialize();
}

/*
eval ("source \"" + `getenv "MAYA_APP_DIR"` + "/library/nkPoseStack/nkPoseStack.mel\"");
nkPoseStack;
*/
