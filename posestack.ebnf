(*! Â© 2023 imaoki | MIT License | https://github.com/imaoki *)
                 file = pose_version , "\n" , poses ;

         pose_version = { digit }+ ;

                poses = { pose , "\n" } ;

                 pose = pose_name , "=" , pose_transforms ;

            pose_name = { any_char - illegal_char }+ ;

      pose_transforms = { pose_transform , ";" }+ ;

       pose_transform = transform_header , transform_values ;

     transform_header = transform_node , "[" , pose_setting , "]" ;

       transform_node = "|" , node_name , { "|" , node_name } ;

            node_name = ( letter | "_" ) , { letter | digit | "_" } ;

         pose_setting = paste_setting , "/"
                      , select_setting , "/"
                      , mirror_setting ;

        paste_setting = paste_x , "," , paste_y , "," , paste_z ;

              paste_x = boolean ;

              paste_y = boolean ;

              paste_z = boolean ;

       select_setting = select_pattern_a , "," , select_pattern_b ;

     select_pattern_a = select_pattern ;

     select_pattern_b = select_pattern ;

       select_pattern = { letter | digit | "_" | "|" }+ ;

       mirror_setting = mirror_axis
                      , "," , primary_axis
                      , "," , secondary_axis
                      , "," , invertPrimary_axis
                      , "," , invertSecondary_axis ;

          mirror_axis = axis ;

         primary_axis = axis ;

       secondary_axis = axis ;

  invert_primary_axis = boolean ;

invert_secondary_axis = boolean ;

                 axis = "0" | "1" | "2" ;

              boolean = "0" | "1" ;

     transform_values = translate
                      , "," , quaternion
                      , "," , scale
                      , "," , shear ;

            translate = number , "," , number , "," , number ;

           quaternion = number , "," , number , "," , number , "," , number ;

                scale = number , "," , number , "," , number ;

                shear = number , "," , number , "," , number ;

               number = [ "-" ] , ( integer | real ) ;

                digit = ? 0-9 ? ;

               letter = ? a-zA-Z ? ;

         illegal_char = "!" | "=" | ";" | "[" | "]" | "/" | "," ;

             any_char = ? Any visible characters ? ;
